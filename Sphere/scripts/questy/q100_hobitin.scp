///:::::Ruzni obyvatele mest apod.:::::///
///:::::Orfeus (Moria - www.moria.cz)::///
///:::::Last update: 20/02/13  Nara::::///
///: Obsahuje:                         ///
///:     1. Zahradnice Ambra           ///           
//////////////////////////////////////////



[speech spk_q100_hobitin_ambra]

on = *ukol*
on = *prace*
on = *zdravim*
on = *ahoj*

if (<distance> > 3)
  src.sysMessage("Na takovou vzdalenost s tebou jednat nebude")
else
  f_q100_hobitin_ambra
endif


[function f_q100_hobitin_ambra]
if (<src.tag.q100_hobitin_ambra_neuspel> !=0)
  say("Prosim ted ne,mam spoustu prace!")
else
dialog(d_q100_hobitin_ambra)

[dialog d_q100_hobitin_ambra]
argo.questDialog_create(03079,<name>)

argo.dialog_newPage("Pozdrav")

argo.questDialog_baseButt(1) // Kdo jsi?
argo.questDialog_baseButt(2,30,"Drby") // Mohu pomoci?
argo.questDialog_baseButt(3) // Obchodovat
argo.questDialog_baseButt(4) // Hledam praci
argo.questDialog_baseButt(5) // Nesu ti
argo.questDialog_baseButt(6) // Prohlednout

argo.dialog_page("Pozdrav")
  if (<src.tag.q100_hobitin_ambra> !=0)  
    argo.setText(<argo.newTextLine>,"Zdravim...")
  else
    argo.setText(<argo.newTextLine>,"Zdravim, jmenuju se Ambra a staram se tady o pole s turinem.")
  endif
  argo.questDialog_HTMLGUMP
  
argo.dialog_page("Kdo jsi?")
  argo.setText(<argo.newTextLine>,"Zdravim, jmenuju se Ambra a staram se tady o pole s turinem.")
  argo.questDialog_HTMLGUMP
  
argo.dialog_page("Drby")
  argo.setText(<argo.newTextLine>,"V tomhle nemuzu slouzit, vetsinou se snazim vypoustet vsechno co slysim, jsou z toho zbytecne problemy.")
  argo.questDialog_HTMLGUMP  
  
argo.dialog_page("Obchodovat")
  argo.setText(<argo.newTextLine>,"Prodala bych ti nejakou zeleninu, ale tahle sklizen je uz bohuzel koupena.")
  argo.questDialog_HTMLGUMP  
  
argo.dialog_page("Hledam praci")
  if (<src.tag.q100_hobitin_ambra> ==2)
    argo.setText(<argo.newTextLine>,"Uz pro tebe zadnou praci nemam.")
    argo.questDialog_HTMLGUMP
  elseif (<src.tag.q100_hobitin_ambra> ==1)
    argo.setText(<argo.newTextLine>,"Mas mi donest ten hnuj.")
    argo.questDialog_HTMLGUMP
  else
    argo.setText(<argo.newTextLine>,"Skutecne? V tom pripade bych pro tebe neco mela. Sama se nemuzu od pole vzdalit, nechci aby mi nekdo ukradl veskerou zeleninu, ale potrebovala bych necim hnojit, kdyz mi naplnis tenhle pytel hnojem, dalo by se uvazovat o nejake odmene *mrkla*. Beres to?")
    argo.questDialog_HTMLGUMP
    argo.setText(argo.newTextLine,"Prijmam")
    argo.questDialog_addResponse(0,1,1,"q100_prijmout")
  endif

  
argo.dialog_page("Nesu ti")
  if (<src.tag.q100_hobitin_ambra>==2)
    argo.setText(<argo.newTextLine>,"Diky, ale uz od tebe nic nepotrebuju.")
    argo.questDialog_HTMLGUMP
  elseif (<src.tag.q100_hobitin_ambra>==1)
    argo.setText(<argo.newTextLine>,"Tak co? Kde mas ten hnuj?")
    argo.questDialog_HTMLGUMP
    
    argo.setText(argo.newTextLine,"Uz ho mam.")
    argo.questDialog_addResponse(0,1,1,"q100_odevzdat")
    argo.setText(argo.newTextLine,"Jeste ho nemam ale snazim se.")
    argo.questDialog_addResponse(0,1,1,"q100_nemam")
    argo.setText(argo.newTextLine,"Kaslu ti na to, sezen si nekoho jineho!!!")
    argo.questDialog_addResponse(0,1,1,"q100_kaslu")
    
  else
    argo.setText(<argo.newTextLine>,"Copak mi neses?")
    argo.questDialog_HTMLGUMP
  endif
  
  
             
argo.dialog_page("Prohlednout")
  argo.setText(<argo.newTextLine>,"Vypada celkem poctive, dela na tebe dojem slusneho hobitiho devcete.")
  argo.questDialog_HTMLGUMP  
  
[dialog d_q100_hobitin_ambra button]
ON=@AnyButton
if (<argn> == argo.dialog_linkButton("q100_odevzdat"))
  if <src.restest i_q100_hobitin_ambra_pytel_2>
    say("Dekuji ti mnohokrat...")
    src.addexp(30)
    src.giveitem(i_fruit_onion)
    src.tag.q100_hobitin_ambra=2
    src.consume(i_q100_hobitin_ambra_pytel_2)
    f_quest_complete(15)  // splnenie questu "Hobitin"
  else
    say("Vzdyt v tom pytli jeste neni hnuj!")
  endif
endif  
  
if (<argn> == argo.dialog_linkButton("q100_nemam"))  
  say("Pospes si!")
endif

if (<argn> == argo.dialog_linkButton("q100_kaslu"))
  say("Dobra...jak chces...")
  src.consume(i_q100_hobitin_ambra_pytel)
  src.consume(i_q100_hobitin_ambra_pytel_2)
  src.tag.remove(q100_hobitin_ambra)
  src.tag(q100_hobitin_ambra_neuspel,1)
endif  

if (<argn> == argo.dialog_linkButton("q100_prijmout"))
  src.giveitem=i_q100_hobitin_ambra_pytel
  src.tag.q100_hobitin_ambra=1  
endif  
  
  

///////Itemy (q100_hobitin_ambra)

[ITEMDEF i_q100_hobitin_ambra_hnuj]
ID=01ebd
NAME=Hnuj
TYPE=t_normal
WEIGHT=4

on=@userDClick
if <src.restest(i_q100_hobitin_ambra_pytel)>
  src.consume(i_q100_hobitin_ambra_pytel)
  src.sysMessage("Nabral si hnuj")
  src.giveitem(i_q100_hobitin_ambra_pytel_2)
else
  src.sysMessage("Nemas prazdny pytel na hnuj")
endif
return 1

on=@create
attr=010
color=007e0


[ITEMDEF i_q100_hobitin_ambra_pytel_2]
ID=01039
NAME=Pytel s hnojem
TYPE=t_normal
WEIGHT=4

on=@create
attr=08010
color=0750

[ITEMDEF i_q100_hobitin_ambra_pytel]
ID=01039
NAME=Pytel na hnuj
TYPE=t_normal
WEIGHT=4

on=@create
attr=08010

