[dialog d_vendor_help]
SetLocation=100,20
argo.tag(sirka,300)
argo.tag(vyska,360)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1,272)
argo.dialog_pozadi(<argo.tag(nexty)>,1,<d_def_button_sirka>,241)
argo.dialog_pozadi(<argo.tag(nexty)>,12,<d_def_button_sirka>,241)
argo.dialog_pozadi(<argo.tag(nexty)>,1,<d_def_button_sirka>,241)
argo.dialog_pozadi(<argo.tag(nexty)>,2,<d_def_button_sirka>,241)
argo.dialog_zpruhledni

argo.texta(<argo.tag(sloupec_x[0])>+1,<argo.tag(obj_y[0])>,d_def_writecolor,"Help - Ovladani vendora")
argo.texta(<argo.tag(sloupec_x[0])>+1,<argo.tag(obj_y[1])>,d_def_readcolor,"Hlp:")
argo.texta(<argo.dialog_textpos(1,1)>,d_def_readcolor,"Osloveni:  Say")

argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+0*<d_def_radek_vyska>-2,4011,4013,1,0,1)	//Stock / Sklad
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+1*<d_def_radek_vyska>,4011,4013,1,0,2)	//Nakup / Buy
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+2*<d_def_radek_vyska>,4011,4013,1,0,3)	//Cena / Price
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+3*<d_def_radek_vyska>,4011,4013,1,0,4)	//Poptavka / Desire
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+4*<d_def_radek_vyska>,4011,4013,1,0,5)	//Penize / Gold
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+5*<d_def_radek_vyska>,4011,4013,1,0,6)	//Pridat / Add
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+6*<d_def_radek_vyska>,4011,4013,1,0,7)	//Zbozi / Collect
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+7*<d_def_radek_vyska>,4011,4013,1,0,8)	//Vydelek
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+8*<d_def_radek_vyska>,4011,4013,1,0,9)	//Oblect / Obleceni / Clothing
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+9*<d_def_radek_vyska>,4011,4013,1,0,10)	//Vlastni / Zradce
argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[2])>+10*<d_def_radek_vyska>,4011,4013,1,0,11)	//Ostatni

argo.settext(100,"'Sklad' , 'Stock'")
argo.settext(101,"'Nakup' , 'Buy', 'Supplies'")
argo.settext(102,"'Cena' , 'Price' , 'Nastavit'")
argo.settext(103,"'Poptavka' , 'Desire'")
argo.settext(104,"'Penize' , 'Penez' , 'Gold'")
argo.settext(105,"'Pridat' , 'Add'")
argo.settext(106,"'Zbozi' , 'Collect'")
argo.settext(107,"'Vydelek'")
argo.settext(108,"'Oblect' , 'Obleceni' , 'Clothing'")
argo.settext(109,"'Vlastni' vs 'Zradce'")
argo.settext(110,"Ostatni...")


argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+0*<d_def_radek_vyska>,d_def_writecolor,100)	//Stock / Sklad
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+1*<d_def_radek_vyska>,d_def_writecolor,101)	//Nakup / Buy
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+2*<d_def_radek_vyska>,d_def_writecolor,102)	//Cena / Price
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+3*<d_def_radek_vyska>,d_def_writecolor,103)	//Poptavka / Desire
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+4*<d_def_radek_vyska>,d_def_writecolor,104)	//Penize / Gold
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+5*<d_def_radek_vyska>,d_def_writecolor,105)	//Pridat / Add
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+6*<d_def_radek_vyska>,d_def_writecolor,106)	//Zbozi / Collect
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+7*<d_def_radek_vyska>,d_def_writecolor,107)	//Vydelek
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+8*<d_def_radek_vyska>,d_def_writecolor,108)	//Oblect / Obleceni / Clothing
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+9*<d_def_radek_vyska>,d_def_writecolor,109)	//Vlastni / Zradce
argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[2])>+10*<d_def_radek_vyska>,d_def_writecolor,110)	//Ostatni

argo.texta(<argo.tag(sloupec_x[0])>+1,<argo.tag(obj_y[3])>,d_def_readcolor,"Hlp:")
argo.texta(<argo.dialog_textpos(3,1)>,d_def_readcolor,"Funkce:  .prikaz")

argo.button(<argo.tag(sloupec_x[0])>,<argo.tag(obj_y[4])>+0*<d_def_radek_vyska>-2,4011,4013,1,0,21)	//VendorAdd

argo.settext(1000,"VendorAdd")

argo.text(<argo.tag(sloupec_x[1])>,<argo.tag(obj_y[4])>+0*<d_def_radek_vyska>,d_def_writecolor,1000)	//VendorAdd


[dialog d_vendor_help BUTTON]
on=1
src.dialog(d_vendor_help)
src.informovat(11,3,"Povel: 'Sklad' nebo 'Stock'",cv_owner_help_stock,400,20)

on=2
src.dialog(d_vendor_help)
src.informovat(11,3,"Povel: 'Nakup' 'Buy' nebo 'Supplies'",cv_owner_help_buy,400,20)

on=3
src.dialog(d_vendor_help)
src.informovat(11,3,"Povel: 'Cena' 'Price' nebo 'Nastavit'",cv_owner_help_price,400,20)

on=4
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Poptavka' nebo 'Desire'",cv_owner_help_desire,400,20)

on=5
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Penize' 'Penez' nebo 'Gold'",cv_owner_help_gold,400,20)

on=6
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Pridat' nebo 'Add'",cv_owner_help_add,400,20)

on=7
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Zbozi' nebo 'Collect'",cv_owner_help_coll,400,20)

on=8
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Vydelek'",cv_owner_help_vydel,400,20)

on=9
src.dialog(d_vendor_help)
src.informovat(11,1,"Povel: 'Oblect' 'Obleceni' nebo 'Clothing'",cv_owner_help_cloth,400,20)

on=10
src.dialog(d_vendor_help)
src.informovat(11,2,"Povel: 'Vlastni' 'Ortodox' nebo 'Zradce' 'Duffer'",cv_owner_help_ort,400,20)

on=11
src.dialog(d_vendor_help)
src.informovat(11,4,Dalsi prikazy,cv_customer_help,400,20)

// FUNKCE
on=21
src.dialog(d_vendor_help)
src.informovat(11,1,Funkce: .VendorAdd,cv_owner_help_fceadd,400,20)


[defnames info]
cv_owner_help_stock[0]	" Kurzorem ukazte na vec ve vasem inventari (nebo zvire ve vasem vlastnictvi), urcene k prodeji. V dalsi tabulce nastavite jeji popis a cenu vepsanim do formulare.<br>"
cv_owner_help_stock[1]	" Pokud chcete prodavat vetsi mnozstvi veci za jednotnou cenu, dejte je do kontejneru, ukazte na nej kurzorem a zaskrtnete 'Pouzit jako jednotkovy kontejner'. Cena kterou zadate je pocitana za jednu kazdou vec v kontejneru.<br>"
cv_owner_help_stock[2]	" Pokud chcete zalozit novou podsekci v nakupnim menu, ukazte kurzorem na kontejner ve vasem vlastnictvi a pouzijte prislusne tlacitko. Pokud jsou v nem nejake veci, budou vam okamzite vraceny a pro sekci bude pouzita ikona prvni z nich.<br>"

cv_owner_help_buy[0]	" V tomto menu muzete vami prodavane veci stahovat z prodeje, v pripade jednotkoveho kontejneru do nej muzete pridavat veci pouzitim prislusneho tlacitka v podmenu a ukazanim na dalsi kontejner, jehoz obsah bude presunut do kontejneru v prodeji.<br>"
cv_owner_help_buy[1]	" Ma-li vendor pridanou alespon jednu polozku pomoci prikazu 'Sklad', pak muzete v tomto menu jednoduse vaseho vendora plnit pomoci prislusnych tlacitek jiz bez nutnosti znovu pouzivat prikazu 'Sklad'.<br>"
cv_owner_help_buy[2]	" NOVA SEKCE: Pokud zacilite libovolny container po stisknuti menu 'pridat dalsi zbozi', prodejni menu vam nabidne moznost 'Nova sekce', ktera pouzije zacileny container jako schranku pro dalsi zbozi. Umozni vam to zprehlednit prodej a rozdelit ho do ruznych kategorii napr. Zbrane, Zbroje, Naradi atp.<br>"

cv_owner_help_price[0]	" Kurzorem ukazte na vec, kterou chcete aby vendor vykupoval, cenu nastavite v dalsim dialogu.<br>"
cv_owner_help_price[1]	" Pokud chcete aby vendor vykupoval tyto veci dokud ma na ne penize, nastavte zaporne cislo do radku Limit. V opacnem pripade napiste do tohoto radku, kolik maximalne danych veci ma vendor vykoupit.<br>"
cv_owner_help_price[2]	" K pridani zbozi pro vykup se dostanete i pres menu vyvolane oslovenim 'Poptavka' nebo 'Desire'<br>"

cv_owner_help_desire[0]	" Kromne seznamu vykupovaneho zbozi slouzi tento dialog i k jeho editaci vykupu.<br>"

cv_owner_help_gold[0]	" Vendor Vas informuje o stavu sveho konta.<br>"

cv_owner_help_add[0]	" Vybranim hromadky minci pridate vendorovi penize na vykup.<br>"

cv_owner_help_coll[0]	" Timto prikazem vam vendor vyda nakoupene zbozi (do banky).<br>"

cv_owner_help_vydel[0]	" Timto prikazem vam vendor vyda penize (do banky).<br>"

cv_owner_help_cloth[0]	" Prikaz pro obleceni vendora. Vymeni to, co mate na sobe Vy za to, co ma na sobe vendor, a to i s vousy a vlasy.<br>"

cv_owner_help_ort[0]	"VLASTNI nebo ORTODOX:<br> timto povelem reknete svemu vendorovi, aby obchodoval pouze se zakazniky stejne strany.<br>"
cv_owner_help_ort[1]	"ZRADCE nebo DUFFER:<br> Penize vytezi ! Kdo ma penize, je nas pritel a s tim bude Vas vendor obchodovat. Nezalezi tedy na rase ani presvedceni zakaznika.<br>"

cv_owner_help_fceadd[0]	" Pouziti: .vendoradd(50000) -> vyskoci vam target -> kliknete na Vaseho vendora a z Banky, mate-li tam potrebnou castku, se presunou penize vendorovi - v tomhle pripade 50k.<br>"

cv_customer_help[0]	Nasledujici prikazy zadavejte vzdy ve tvaru '(jmeno vendora) (prikaz)'
cv_customer_help[1]	'NAKUP','KUP','SUPPLIES','WARES'<br>nabidka veci ktere ma vendor na prodej. Text u jednotliveho zbozi na teto strance nemusi odpovidat skutecnemu nazvu ci mnozstvi veci. Na potvrzovacim dialogu v zahlavi se doctete skutecne mnozstvi a jmeno prodavane veci. V pripade kontejneru muzete zkontrolovat jejich obsah prislusnym tlacitkem, stejne jako u jednotkoveho prodeje, kde si muzte nastavit primo do dialogu kolik veci z kontejneru kupujete.
cv_customer_help[2]	'POPTAV','DESIRE'<br>seznam veci vykupovanych vendorem i s jejich cenami a maximem akceptovanych kusu.
cv_customer_help[3]	'KDO','KOMU'<br>Informuje vas, kdo je zamestnavatelem vendora.

[events e_vendor_custom]
on=@gethit
FLAGS=statf_invul|statf_freeze
return 1

on=@userclick
messagecol(<eval finduid(tag(mainowner)).getnamehue>,<name>)
return 1

on=@userdclick
title=Zamestnavatel prodejce: <?tag(mainowner).name?>

[typedef t_deed_vendor]
ON=@userDClick
src.NEWNPC=<more1>
src.act.tag(mainowner,<src>)
src.act.FOOD=<src.act.def.maxfood>
src.act.P=<src.P>
src.act.UPDATE
src.act.newmemory(<src>)
src.act.act.color=memory_ipet
remove
RETURN 1

[events e_vendor_check]
on=@step
tag.remove(dialog_vendor_check)
events=-e_vendor_check

on=@spellcast
tag.remove(dialog_vendor_check)
events=-e_vendor_check

[ITEMDEF i_deed_vendor]
ID=i_deed
TYPE=t_deed_vendor
name=Vendor Deed (male)

on=@create
more1=c_h_vendor

[ITEMDEF i_deed_vendor_f]
ID=i_deed
TYPE=t_deed_vendor
name=Vendor Deed (female)

on=@create
more1=c_h_vendor_f

[function checkVendorDialog]
if (strlen(<src.tag(dialog_vendor_check)>))
  if (<src.tag(dialog_vendor_check)> != <topobj>)
    src.sysMessage("Ne, jednani se dvema obchodniky zaroven je neslusne.")
    return 1
  endif
endif
return 0

[function checkvendor]
if !(strlen(<src.tag(dialog_vendor_check)>))
  src.tag(dialog_vendor_check,<uid>)
endif
src.events=+e_vendor_check
dex=0
food=2000	//<typedef.maxfood>
if !(strlen(<tag.gold>))
  tag(gold,0)
endif
if !(strlen(<tag.provize>))
  tag(provize,10)
endif

if !(findID(i_vendor_box))
 newitem i_vendor_box
 act.cont=<UID>
endif
if !(findID(i_vendor_tag_box))
 newitem i_vendor_tag_box
 act.cont=<UID>
endif
if !(findID(i_buyer_box))
 newitem i_buyer_box
 act.cont=<UID>
endif
if !(findID(i_buyer_goods_box))
 newitem i_buyer_goods_box
 act.cont=<UID>
endif
if !(findlayer(29))
 newitem i_bankbox
 act.cont=<UID>
endif

[function isVendorInactive]
if (checkVendorDialog) || !(src.isEvent(e_vendor_check))
  return 1
else
  return 0
endif


[SPEECH spk_vendor_custom]
on=*forget*
on=*zapomen*
say(Provedu)
if (ismypet)&&(!src.isgm)
 //kermel.sysmessage("mypet: <src.name>, mainowner: <tag(mainowner)>")
 tag(mainowner,<src>)
 //kermel.sysmessage("mainowner ted: <tag(mainowner)>")
endif
if (src==tag(mainowner))
 while (memoryfindtype(memory_ipet))
 safe memoryfindtype(memory_ipet).remove
 endwhile
 newmemory(<src>)
 act.color=memory_ipet
 act.timer=-1
endif
ON=*inventory*
checkvendor
if (src.isgm)
  findID(i_vendor_box).drop
  findID(i_vendor_tag_box).drop
  findID(i_buyer_box).drop
  findID(i_buyer_goods_box).drop
endif
ON=*kdo*
ON=*komu*
if(strlen(<memoryfindtype(memory_ipet).link.region.tag(houserealm)>))
  say "Mym zamestavatelem je <?memoryfindtype(memory_ipet).link.sex(pan,pani)?> <?memoryfindtype(memory_ipet).link.getrealnamebyrealm(<region.tag.houserealm>)?>."
else
  say "Mym zamestavatelem je <?memoryfindtype(memory_ipet).link.sex(pan,pani)?> <?memoryfindtype(memory_ipet).link.name?>."
endif
ON=*oblect*
on=*obleceni*
on=*clothing*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
say(Hned se prevleknu)
if (restrictside)
 return 1
endif
checkvendor
if (isMyPet)
 eqvendorlayers(cont=<findlayer(29)>)
 src.eqvendorlayers(logcont=<uid>)
 findlayer(29).contents(logcont(<src>))//cont=<src>
endif
ON=*nakup*
ON=*kup*
ON=*buy*
ON=*supplies*
ON=*wares*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
food=<def.maxfood>
say(Maly moment)
if (restrictside)
 return 1
endif
if (checkVendorDialog) //neni otevrene menu jineho vendora?
  return 1
endif
checkvendor
if (<findID(i_vendor_tag_box).rescount>)
 src.tag(cv_lastdialog,"")
 findID(i_vendor_tag_box).DIALOG d_vendor
else
 say Nemam nic na prodej.
endif

ON=*stock* 
ON=*sklad*
say(Okamzicek)
if (restrictside)
 return 1
endif
if (checkvendordialog) //neni otevrene menu jineho vendora?
  return 1
endif
checkvendor
if (isMyPet)
 safe src.findID(i_vendor_stock).remove
 src.newitem i_vendor_stock
 src.act.link=<findID(i_vendor_tag_box)>
 src.act.cont=<src>
 src.act.target("Target an item in your pack to stock.")
endif

ON=*cena*
ON=*price*
ON=*nastavit*
if (restrictside)
 return 1
endif
checkvendor
setprice
on=*gold*
on=*penez*
on=*penize*
say(Pockej, spocitam to)
if (restrictside)
 return 1
endif
if (ismypet)
say Mam k dispozici <tag.gold> zlata.
endif
ON=*vydelek*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
if (restrictside)
 return 1
endif
say(Hned to bude...)
checkvendor
if (ISMYPET)
 if !(<tag.GOLD>)
  say Dnes pro tebe nemam zadne penize...
 else
  src.findlayer(29).givegold(<tag.gold>)
  say Dostal jsi <tag.gold> zlata.
  tag(gold,0)
 endif
endif
on=*zbozi*
ON=*collect*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
if (restrictside)
 return 1
endif
say(Hned to bude...)
checkvendor
if (ISMYPET)
 if (findID(i_buyer_goods_box).rescount)
  newitem i_backpack
  act.name="Collected from <?name?>"
  act.logcont=<src.findlayer(29)>
  findID(i_buyer_goods_box).contents(collectgoods(<act>))
  say Nakoupene zbozi najdes v bance.
 else
  say Dnes pro tebe nemam zadne zbozi.
 endif
endif
ON=*Redeed*
if (src.isgm)
 src.newitemsafe(i_backpack)
 lastnew.name="From redeeded vendor <?name?>"
 lastnew.logbounce 
 lastnew.givegold(<tag.gold>)
 findID(i_vendor_box).contents2(f_safeitem_restore(<lastnew>))
 findID(i_buyer_goods_box).contents(collectgoods(<lastnew>))
 eqlayers(logcont=<lastnew>)
 src.NEWITEMsafe(i_deed_vendor)
 src.act.logcont=<lastnew>
 remove
endif
on=*pridat*
on=*add*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
if (restrictside)
 return 1
endif
checkvendor
if (ISMYPET)
 safe src.findID(i_vendor_gold).remove
 src.newitem i_vendor_gold
 src.act.link=<UID>
 src.act.cont=<src>
 src.act.TARGET Target gold to add to vendors account.
endif
on=*sell*
on=*prod*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
say(Ukaz co mas)
if (restrictside)
 return 1
endif
if (checkvendordialog) //neni otevrene menu jineho vendora?
  return 1
endif
checkvendor
vendorspk_sell
on=*poptav*
on=*desire*
if (src.isdead)
	src.redmessage("Jako duch tohle nemuzes!")
	return 1
endif
say(Shanim toto zbozi)
if (restrictside)
 return 1
endif
if (checkvendordialog) //neni otevrene menu jineho vendora?
  return 1
endif
checkvendor
src.tag(cv_lastdialog,"")
findID(i_buyer_box).dialog d_vendor_desire
on=*info*
on=*napove*
on=*help*
if (restrictside)
 return 1
endif
if (ismypet)
 src.Dialog(d_vendor_help)
else
 src.informovat(16,4,Napoveda k ovladani vendora,cv_customer_help)
endif
//mandos
ON=*ortodox*
ON=*vlast*
checkvendor
if (ISMYPET)
 tag.pure=1
 SAY "S neprateli meho pana nejednam !"
endif
ON=*zrad*
ON=*duffer*
checkvendor
if (ISMYPET)
 tag.pure=0
 SAY "Kdo ma penize, je nas pritel."
endif
//newitem i_infobook
//act.logbounce
//act.more=b_vendor_navod_<eval <ismypet>+1>
//act.dclick

[function restrictside]
if(!tag(pure))
 return 0
else
 if (<src.tag(realm)>==<finduid(<tag(mainowner)>).tag(realm)>)||(src.isgm)
  //kermel.sysmessage(src:<src.tag(realm)>, maj:<finduid(<tag(mainowner)>).tag(realm)>)
  return 0
 else
  say "S takovymi jako jsi ty nejednam!"
  return 1
 endif 
endif


[function collectgoods]
f_safeitem_restore
if (tag(alive))
 dclick
else
 logcont=<args>
endif

[function setprice]
if (isMyPet)
 safe src.findID(i_vendor_price).remove
 src.newitem i_vendor_price
 src.act.link=<uid>
 src.act.cont=<src>
 src.act.target("Target an item to set its price.")
 return 1
else
 return 0
endif

[function vendorspk_sell]//def: vendor
safe findID(i_buyer_tag_item).remove
newitem(i_buyer_tag_item)
equip(lastnew)
safe src.findID(i_buyer_tag_box).remove
src.newitem i_buyer_tag_box
lastnew.name="<?lastnew.name?> - <?region.name?>" // napr. "- stanek 30"
src.equip(<src.act>)
src.act.link=<uid>
findID(i_buyer_box).contents(extractprice)
src.findlayer(21).contents(trysellit)
src.tag(cv_lastdialog,"")
src.findID(i_buyer_tag_box).dialog d_vendor_sell

[function extractprice]
topobj.findID(i_buyer_tag_item).tag(id_<tag.realid>_limit,<tag.limit>)
topobj.findID(i_buyer_tag_item).tag(id_<tag.realid>_price,<tag.price>)
topobj.findID(i_buyer_tag_item).tag(id_<tag.realid>_listuid,<uid>)

[function trysellit]//def:kazdej item v hracove batohu
if ((type==t_container)&&(safe rescount))
 src.newitem <baseid>
 arg(thisbag,<lastnew>)
 lastnew.link=<uid>
 lastnew.tag(oamount,<amount>)
 lastnew.name="<name> (<rescount> ks)"
 lastnew.dispID=<dispID>
 lastnew.cont=<src.findID(i_buyer_tag_box)>
 lastnew.tag(contprice,<eval src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_price)>)
 lastnew.tag(listuid,<src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_listuid)>)
 var(price,0)
 contents2(getprice(<lastnew>))
 arg(thisbag).tag(price,<eval arg(thisbag).tag(contprice)+var(price)>)
 if !(arg(thisbag).tag(price))//nulova cena, nechtit.
  arg(thisbag).remove
 endif
elseif (src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_price))
 var(price,0)
 getprice(<src.findID(i_buyer_tag_box)>)
 lastnew.tag(limit,<src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_limit)>)
endif

[function getprice]//kontejner kam to hodit
if (<attr>&0b0d4)
 return 0
endif
arg(thisprice,<eval <eval src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_price)>*<amount>)>)
if (arg(thisprice)) //only compute if we also want the keg as such
	arg(bottleprice,<eval src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_i_bottle_empty_price)>)
	arg(potionprice,<eval src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<tag.id>_price)>)
	if ((baseid==i_keg_potion) && (arg(bottleprice)) && (arg(potionprice)))
		arg(unitprice,<eval arg(potionprice)-arg(bottleprice)>)
		if (<arg(unitprice)> > 0)
			arg(thisprice,<eval arg(thisprice)+(arg(unitprice)*<morex>)>)
		endif
	endif
endif
if (arg(thisprice))
 src.newitem i_vendor_item
 lastnew.name="<?qval(<amount>>1,"<amount> ",)?><?name?>"
 lastnew.link=<uid>
 lastnew.tag(oamount,<amount>)
 lastnew.dispID=<dispID>
 lastnew.color=<color>
 lastnew.cont=<args>
 lastnew.tag(price,<arg(thisprice)>)
 lastnew.tag(listuid,<src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_listuid)>)
 var(price,#+<arg(thisprice)>)
endif
return <arg(thisprice)>

[function cv_radku]//rescount	def:argo
arg(itemu,<args>)
if ((itemu-tag(firsti))>cv_dialog_maxradku)
 tag(radku,<cv_dialog_maxradku>)
else
 tag(radku,<eval itemu-tag(firsti)>)
endif

[defnames custvendor]
cv_dialog_sirka		640
cv_dialog_maxradku	15
cv_dialog_radek_vyska	30

[DIALOG d_vendor_forget]
arg(textcolor,52)
argo.SetLocation=285,250

argo.tag(sirka,400)
argo.tag(vyska,265)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,10,133,238)
argo.dialog_pozadi(<argo.tag(nexty)>,0,133,238)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,2301,Friends)

arg(i,0)
while (memoryfindtype(memory_ipet)) && (<i> < 10)
  src.sysMessage("cykl <i>")
  arg(petMemory_<i>,<memoryFindType(memory_ipet)>)
  arg(i,#+1)
  finduid(<arg(petMemory_<i>)>).p=<src.p>
endwhile
arg(friendCount,<i>)
arg(i,0)
argo.dialog_textpos(1,0)
while (<i> < <friendCount>)
  src.sysMessage("druhycykl <i>")
  argo.texta(<lastxpos>,<lastypos>+<i>*<d_def_radek_vyska>,2301,"memory <i> :")
  argo.texta(<lastxpos>+140,<lastypos>+<i>*<d_def_radek_vyska>,2301,<arg(petMemory_<i>)>)
  equip(<arg(petMemory_<i>)>)
  arg(i,#+1)
endwhile

// TODO: remove buttony dialogu + linknuti do tabulky vendora

[DIALOG d_vendor]//def: i_vendor_tag_box
SetLocation=10,10
argo.tag(firsti,<eval argv(0)>)//<eval argv(1)>
if (argo.tag(firsti)<0)
 argo.tag(firsti,0)
endif
argo.cv_radku(<rescount>)
argo.tag(sirka,<cv_dialog_sirka>)
argo.tag(vyska,<eval (3*<d_def_radek_vyska>)+(argo.tag(radku)*cv_dialog_radek_vyska)+(4*<d_def_skvira>)+(2*<d_def_okraj>)>)

argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)

var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
var(sloupec_noback[0],1)
argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,20,40,450)
var(sloupec_noback[0],"")
var(d_def_radek_vyska,"")
argo.dialog_pozadi(<argo.tag(nexty)>,1)
var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
argo.dialog_zpruhledni

if (cont) //if not, its some error
 arg(hobj,<uid>)
 while (<eval arg(hobj).cont>!=<eval arg(hobj).topobj>)
  arg(urovne_uid,"<safe urovne_uid><arg(hobj)>,")
  arg(hobj,<arg(hobj).cont>)
 endwhile
 arg(urovne_uid,"<safe urovne_uid><arg(hobj)>")
 argo.texta(<argo.dialog_textpos(0,0)>,2301,<cm_UIDs_list(<arg(urovne_uid)>)>)
endif


argo.texta(<argo.dialog_textpos(1,0,1)>,2301,kup)
argo.texta(<argo.dialog_textpos(1,2)>,2301,Nazev zbozi)
argo.texta(<argo.dialog_textpos(1,3)>,2301,Cena/gp)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,9)//cancel

if (argo.tag(firsti))
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[2])>+1,0fa,0fb,1,0,1) // prev
endif
arg(imax,<argo.tag(firsti)>+<cv_dialog_maxradku>)
if (imax>rescount)
 arg(imax,<rescount>)
else
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[3])>-24,0fc,0fd,1,0,2) // next
endif

arg(ypos,<argo.tag(obj_y[2])>)
arg(index,<argo.tag(firsti)>)
while (index<imax)
 if (topobj.ismypet)
  argo.button(<argo.tag(sloupec_x[3])>-35,ypos,0984,0983,1,0,(index*3)+101) 
  argo.button(<argo.tag(sloupec_x[3])>-20,ypos,0984,0984,1,0,(index*3)+102)
  argo.button(<argo.tag(sloupec_x[3])>-20,ypos+8,0983,0983,1,0,(index*3)+102)
 endif
 arg(item,<findcont(index)>)
 argo.button(<argo.tag(sloupec_x[0])>,ypos,9905,9904,1,0,(index*3)+103)
 argo.tilepic(<argo.tag(sloupec_x[1])>,ypos,<arg(item).dispiddec)>)
 argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(description)>")
 argo.textA(<argo.tag(sloupec_x[3])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(price)>")
 arg(ypos,#+<d_def_radek_vyska>)
 arg(index,#+1)
endwhile

if (topobj.ismypet)//ovladaci tlacitka
 argo.dialog_textpos(3,0,1)
 argo.button(lastxpos,lastypos-1,9905,9904,1,0,3)
 argo.textA(lastxpos+30,lastypos,2301,Pridat dalsi zbozi)
 if !(rescount)
  argo.button(lastxpos+200,lastypos-1,9905,9904,1,0,4)
  argo.textA(lastxpos+230,lastypos,2301,Odebrat sekci)
 endif
 argo.button(lastxpos+400,lastypos-1,9905,9904,1,0,5)
 argo.textA(lastxpos+430,lastypos,2301,Prevratit nabidku)
 if (topobj!=cont)//vlozena sekce
   argo.button(lastxpos+560,lastypos-1,9905,9904,1,0,6)
   argo.textA(lastxpos+580,lastypos,2301,Ikona)
 endif
else
 argo.texta(<argo.dialog_textpos(3,0)>,2301,Vase konto: <eval src.findlayer(21).rescount(t_gold)+src.bankbalance> gp)
endif
var(d_def_radek_vyska,"")

[DIALOG d_vendor text]
[DIALOG d_vendor BUTTON]
on=0
//safe(<src.tag.cv_lastdialog>)
//src.tag.cv_lastdialog=
if (<cont>!=<topobj>)
 cont.dialog d_vendor
endif

on=1//prev
if (isVendorInactive)
  return 1
endif
DIALOG(d_vendor,<argo.tag(firsti)>-cv_dialog_maxradku)

on=2//next
if (isVendorInactive)
  return 1
endif
DIALOG(d_vendor,<argo.tag(firsti)>+<argo.tag(radku)>)
 
on=3//stock item
if (isVendorInactive)
  return 1
endif
if (topobj.ISMYPET)
 safe src.findID(i_vendor_stock).remove
 src.newitem i_vendor_stock
 src.act.link=<uid>
 src.act.cont=<src>
 src.act.target("Target an item in your pack to stock.")
 src.tag(cv_lastdialog,"finduid(<uid>).dialog(d_vendor,<argo.tag(firsti)>)")
endif

on=4//smazat sekci
if (isVendorInactive)
  return 1
endif
if (topobj.ismypet)
 arg(mycont,<cont>)
 dispID=<typedef.dispID>
 contents(link.logbounce)
 emptycont
 logbounce
 tag(description,"")
 arg(mycont).dialog(d_vendor)
endif

on=5//
if (isVendorInactive)
  return 1
endif
contreverse
dialog(d_vendor,<argo.tag(firsti)>)

on=6//zmena ikony
if (topobj.ISMYPET)
 safe src.findID(i_vendor_changeicon).remove
 src.newitem i_vendor_changeicon
 src.act.link=<UID>
 src.act.cont=<src>
 src.act.TARGET target something to copy icon from.
 src.tag(cv_lastdialog,"finduid(<uid>).dialog(d_vendor,<argo.tag(firsti)>)")
endif

on=9
return

on=@anybutton
if (isVendorInactive)
  return 1
endif
arg(radek,<eval (argn-101)/3>)
arg(item,<findcont(radek)>)
arg(tlacitko,<eval (argn-100)%3>)
//src.smsg radek cislo <radek> item <arg(item).name> tlacitko <tlacitko>
if (tlacitko==0)
 if (arg(item).tag(section))
  arg(item).dialog d_vendor
 else
  src.tag(cv_lastdialog,"finduid(<uid>).dialog(d_vendor,<?argo.tag(firsti)?>)")
  arg(item).DIALOG d_vendor_item
 endif
elseif (tlacitko==1)
 contmoveup(<radek>)
 dialog(d_vendor,<argo.tag(firsti)>)
else
 contmovetop(<radek>)
 dialog(d_vendor,<argo.tag(firsti)>)
endif
//endif

[ITEMDEF i_vendor_changeicon]
ID=i_memory
TYPE=t_eq_script
name=Vendor iconchanger

ON=@Create
 TIMER=60
 
on=@timer
remove
return 1

ON=@TargOn_Char
link.dispiddec=<src.targ.typedef.icon>
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")
remove
RETURN 1

ON=@TargOn_Item
link.dispiddec=<src.targ.dispiddec>
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")
remove
RETURN 1

[function contreverse]
arg(count,<rescount>)
arg(u,0)
src.newitemsafe(i_bag)
while (findcont(0))
 findcont(0).cont=<lastnew>
 arg(u,<arg(u)>+1)
endwhile
lastnew.contents(cont=<uid>)
lastnew.remove

[function contmovetop]
//src.smsg movetop
arg(item,<findcont(<args>)>)
arg(item).p=1,1
arg(item).cont=<uid>

[function contmoveup]
//src.smsg moveup
arg(muitemindex,<args>)
src.newitemsafe(i_bag)
findcont(<arg(muitemindex)>).cont=<lastnew>
arg(muitemindex,#-2)
while (muitemindex>=0)
findcont(<arg(muitemindex)>).cont=<lastnew>
arg(muitemindex,#-1)
endwhile
lastnew.contents(cont=<uid>)
lastnew.remove

[DIALOG d_vendor_item]
SetLocation=100,100
argo.tag(sirka,300)
argo.tag(vyska,<eval (8*d_def_radek_vyska)+(7*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,3)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_zpruhledni

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,0)
argo.settext(100,<tag(description)>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.HTMLGUMP(<argo.dialog_textpos(1,0)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),3*d_def_radek_vyska,100,0,1)

argo.texta(<argo.dialog_textpos(0,0)>,0481,<amount> <name>)
argo.texta(<argo.dialog_textpos(2,0)>,0481,Barva)
argo.texta(<argo.dialog_textpos(2,1)>,<EVAL <color>-1>,"||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||")
argo.texta(<argo.dialog_textpos(3,0)>,0481,Cena)
settext(105,<tag.PRICE>)
if (topobj.ismypet)
 argo.textentry(<argo.dialog_textpos(3,1)>,100,d_def_radek_vyska,0481,1,105)
 argo.button(lastxpos+100,lastypos,4005,4007,1,0,4)//zmenit cenu
 argo.texta(lastxpos+135,lastypos,0481,Zmenit)//zmenit cenu 
else
 argo.text(<argo.dialog_textpos(3,1)>,0481,105) 
endif
if (tag(jednot))
 argo.texta(<argo.dialog_textpos(4,0)>,0481,Jednotek?)
 if (topobj.ismypet)
  argo.dialog_textpos(5,0,1)
  argo.button(lastxpos+100,lastypos,4005,4007,1,0,3)//
  argo.texta(lastxpos+135,lastypos,0481,Pridat)//
  argo.settext(110,)
 else
  argo.settext(110,1)
 endif
 argo.textentry(<argo.dialog_textpos(4,1)>,100,d_def_radek_vyska,0481,0,110)
 argo.texta(lastxpos+100,lastypos,0481,<link.countamount>)
endif

argo.button(<argo.dialog_textpos(5,0,1)>,4005,4007,1,0,1)//zmenit cenu
if (topobj.ismypet)
 argo.texta(lastxpos+35,lastypos,0481,Stahnout)
else
 argo.texta(lastxpos+35,lastypos,0481,Koupit)
endif
if (link.type==t_container)&&(safe link.rescount) && !(link.id==i_truhla_heslo)//
 argo.button(lastxpos+190,lastypos,4005,4007,1,0,2)//
 argo.texta(lastxpos+225,lastypos,0481,Projit)//
 return -2
 break
endif

if (link.cont.baseid==i_pouch)
 if (link.cont.rescount==1) && !(link.id==i_truhla_heslo)//if (cont.baseid==i_pouch)&&(cont.rescount==1)
  argo.button(lastxpos+190,lastypos,4005,4007,1,0,5)//
  argo.texta(lastxpos+221,lastypos,0481,Prohledni)//
 endif
endif

[DIALOG d_vendor_item TEXT]

[DIALOG d_vendor_item BUTTON]
on=0
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

on=1//stahnout/koupit
if (isVendorInactive)
  return 1
endif
if (TOPOBJ.ISMYPET)
 if (link.tag(alive))
  TOPOBJ.say Zvire vraceno.
  link.f_safeitem_restore
  link.logbounce
  link.dclick
 elseif (tag(jednot))&&(strlen(<argtxt(0)>))
  arg(predat,<?fixNumber(<argtxt(0)>)?>)
  if (arg(predat)<=0)
   arg(predat,1)
  endif
  if (arg(predat)>link.countamount)
   arg(predat,<link.countamount>)
  endif
  link.contents2(f_safeitem_restore)
  link.pv_giveitemunit(<arg(predat)>)
  tag(description,"<?link.countamount?>x <?tag.unitdescription?>")
  safe(<src.tag.cv_lastdialog>)
  src.tag(cv_lastdialog,"")
  return 1
 else
  TOPOBJ.say Vec vracena do tveho batohu.
  link.pv_giveitem
 endif
 remove
 safe(<src.tag.cv_lastdialog>)
 src.tag(cv_lastdialog,"")
 RETURN 0
else
 if (tag(jednot))
  arg(predat,<fixNumber(<argtxt(0)>)>)
  if (arg(predat)<=0)
   arg(predat,1)
  endif
  if (arg(predat)>link.countamount)
   arg(predat,<link.countamount>)
  endif
  var(jednotprice,<eval tag(price)*arg(predat)>)
  if (src.pay(<var.jednotprice>))
   TOPOBJ.tag(GOLD,<eval <TOPOBJ.tag.GOLD>+((<var.jednotprice>*(100-<topobj.tag.provize>))/100)>)
   link.contents2(f_safeitem_restore)
   link.pv_giveitemunit(<arg(predat)>)
   tag(description,"<?link.countamount?>x <?tag.unitdescription?>")
  endif
  safe(<src.tag.cv_lastdialog>)
  src.tag(cv_lastdialog,"")
  return 0
 else
  if (src.pay(<tag.PRICE>)) 
   TOPOBJ.tag(GOLD,<eval <TOPOBJ.tag.GOLD>+((<tag.PRICE>*(100-<topobj.tag.provize>))/100)>)
   TOPOBJ.say Dekuji za obchod.
   if (link.tag(alive))
    link.f_safeitem_restore
    link.logbounce
    link.dclick
   else
    link.pv_giveitem
   endif
   remove
   safe(<src.tag.cv_lastdialog>)
   src.tag(cv_lastdialog,"")
  endif
 endif
endif
return 1

on=2//projit container ->
if !(src.isGM)
  src.redMessage("Funkce prozatim zakazana ! Doslo k zneuziti.")
  dialog d_vendor_item
  return 0
endif
if (isVendorInactive)
  return 1
endif
if (safe link.rescount)
 link.f_safeitem
 link.bounce
 link.open
 link.contents2(f_safeitem)
 link.cont=<topobj.findID(i_vendor_box)>
else
 src.sysMessage("Kontejner je prazdny.")
endif
dialog d_vendor_item
    
on=3//pridat jednotky
if (isVendorInactive)
  return 1
endif
safe src.findID(i_vendor_addunit).remove
src.newitem i_vendor_addunit
src.act.equip
src.act.link=<uid>
src.act.tag(currentAmount,<link.rescount>)
src.act.target("Zamer kontejner ze ktereho maji byt pridany veci.")

on=4//nastavit cenu
if (isVendorInactive)
  return 1
endif
tag(price,<fixNumberPositive(<argtxt(1)>)>)
cont.dialog d_vendor

on=5//prohlednout vec
if !(src.isGM)
  src.redMessage("Funkce prozatim zakazana ! Doslo k zneuziti.")
  dialog d_vendor_item
  return 0
endif
if (isVendorInactive)
  return 1
endif
if (safe link.cont.rescount)
 link.cont.f_safeitem
 link.cont.contents2(f_safeitem)
 link.cont.bounce
 link.cont.open
 link.cont.cont=<topobj.findID(i_vendor_box)>
else
 src.sysMessage("Kontejner je prazdny - asi chybka.")
endif
dialog d_vendor_item

[ITEMDEF i_vendor_addunit]
ID=i_memory
TYPE=t_eq_script
name=add unit Stock

ON=@Create
ATTR attr_decay
TIMER=60

ON=@TargOn_Char
timer=60
target("Zamer kontejner ze ktereho maji byt pridany veci.")
return 1

ON=@TargOn_Item
if (src.targ.topobj.uid==src.uid)&&(src.targ.cont.uid!=src.uid)&&(src.targ.type==t_container)
 if (src.targ.ID==i_truhla_heslo) //vyjimky
  src.redmessage("Obsah tohoto containeru nelze pridat.")
  timer=60
  target("Zamer kontejner ze ktereho maji byt pridany veci.")
  return 1
 else
  if ((<src.targ.rescount>+<tag(currentAmount)>) > 500)
   src.targ.contentsToCont(<link.link>,500-<tag(currentAmount)>)
   src.redMessage("V jedne polozce smis prodavat nejvyse 500ks nestuckovatelnych predmetu.")
  else
   src.targ.contents(cont=<link.link>)
  endif
  link.link.removefromview	//oprava foxova bugu ;) pridanou jednotku slo z baglu vyndat pokud si ho clovek zrovna prohlizel
  link.tag(description,"<?link.link.countamount?>x <?link.tag.unitdescription?>")
  src.targ.updatex
  link.dialog d_vendor_item
 endif
else
 timer=60
 target("Zamer kontejner ze ktereho maji byt pridany veci.")
 return 1
endif
remove
return 1

[function pv_giveitemunit]//bounces <args> of items from this container to src`s bag
//Takze takhle bych si to predstavoval ja :PPP snad sem podchytil vsechno co muze nastat ;)

//no nic moc, resis spousta veci ktery uz sou vyreseny pred volanim ty funkce nebo napsany jinde

//arg(tomove,<eval args>)
//arg(initems,<rescount>)
//while (arg(initems)>0)
// arg(initems,#-1)
// arg(initemscelkem,<arg(initemscelkem)>+<findcont(<arg(initems)>).amount>)
//endwhile
//if (arg(initemscelkem)<=arg(tomove))
// arg(tomove,<arg(initemscelkem)>)
//endif
//while (arg(tomove)>0)
// arg(initems,<rescount>)
//  while (arg(initems)>0)
//   arg(initems,#-1)
//    if (<findcont(<arg(initems)>).amount>==<arg(tomove)>)
//     arg(tomove,#-findcont(<arg(initems)>).amount)
//     findcont(<arg(initems)>).logbounce
//     arg(initems,0)
//    elseif (<findcont(<arg(initems)>).amount><<arg(tomove)>)
//     arg(tomove,#-findcont(<arg(initems)>).amount)
//     findcont(<arg(initems)>).logbounce
//    elseif (<findcont(<arg(initems)>).amount>><arg(tomove)>)
//     findcont(<arg(initems)>).amount=<findcont(<arg(initems)>).amount>-<arg(tomove)>
//     src.newitem <findcont(0).id>, <arg(tomove)>
//     lastnew.bounce
//     arg(tomove,0)
//     arg(initems,0)
//    endif
//  endwhile
//endwhile


arg(tomove,<eval args>)
while (arg(tomove)>0) //proc loop??? protoze to taky muze znamenat x kusu po jednom. fajn ale ted to dava 0 ks :P
  if (findcont(0).amount<=arg(tomove))
    arg(tomove,#-findcont(0).amount)
    findcont(0).logbounce
  else//debug PRISERNY RESENI...existuje lepsi? :)
  //arg(tomove,0) //todle je vazne moc sikovny ;)))
    arg(itemtodupe,<findcont(0)>)//the item to dupe
    arg(backamount,<eval arg(itemtodupe).amount-arg(tomove)>)
    src.newitem i_bag
    lastnew.cont=<uid>
    arg(itemtodupe).cont=<lastnew>
  //the item to dupe is now in a bag
    lastnew.dupe
  //and duped
    findcont(0).findcont(0).amount=<arg(tomove)>
  //amount is set to the what is needed
    findcont(0).findcont(0).logbounce
    findcont(0).remove
  //the duped bag is removed
    findcont(0).findcont(0).amount=<arg(backamount)>
    arg(bagtoremove,<findcont(0)>)
    findcont(0).findcont(0).cont=<uid>
    arg(bagtoremove).remove
    arg(tomove,0)//presunuto sem
  endif
endwhile

[function pv_giveitem]
f_safeitem_restore
if (iscontainer)
 contents2(f_safeitem_restore)
endif
if (cont.baseid==i_pouch)&&(cont.rescount==1)
arg(contuid,<cont>)
logbounce
arg(contuid).remove
else
logbounce
endif

[function vendoradd]
if (argvcount == 0)
  RedMessage("Musis zadat hodnotu v gp, kterou chces vendorovi dat. ")
  SysMessage("Priklad spravneho pouziti: .vendoradd(5000)")
else
  newequip(i_vendor_gold_bank)
  lastnew.tag(gold,<argv(0)>)
endif

[ITEMDEF i_vendor_gold_bank]
ID=i_memory
TYPE=t_eq_script
name=Vendor gold

ON=@Create
ATTR attr_decay
TIMER=60

ON=@Equip
Target("Kteremu vendorovi chces pridat penize?")

ON=@TargOn_Char
arg(vendor,<src.targ>)
if (vendor.oBody == c_h_vendor)
  if (vendor.restrictside)
    remove
    return 1
  endif
  vendor.checkvendor
  if (vendor.ISMYPET)
    if (src.pay_from_bank(<tag.gold>))
      src.targ.tag(gold,#+<tag.gold>)
      src.targ.say("Dekuji. Penize jsem si ulozil.")
    endif
    remove
    return 1
  else
    src.RedMessage("Toto neni Tvuj vendor !")
    Target
    return 1
  endif
else
  src.RedMessage("Toto neni vendor !")
  Target
  return 1
endif
RETURN 1

ON=@TargOn_Item
src.RedMessage("Toto neni vendor !")
Target
RETURN 1

ON=@TargOn_Ground
src.RedMessage("Tady nic nevidis.")
Target
RETURN 1

[ITEMDEF i_vendor_gold]
ID=i_memory
TYPE=t_eq_script
name=Vendor gold

ON=@Create
ATTR attr_decay
TIMER=60

ON=@TargOn_Char
TIMER=60
TARGET Target gold to add to vendors account.
RETURN 1

ON=@TargOn_Item
if (link.isVendorInactive)
  return 0
endif
if (src.TARG.TOPOBJ==src)
  if (src.targ.type==t_gold)
    link.tag(gold,#+<src.targ.amount>)
    src.targ.remove
    return 1
  elseif (src.targ.baseid==i_cashiers_check)
    link.tag(gold,#+<src.targ.MORE>)
    src.targ.remove
    return 1
  endif
endif
TIMER=60
TARGET
RETURN 1

[function iscvendor]
if (npcExists(<uid>))
  if (baseid==c_h_vendor)||(baseid==c_h_vendor_f)
    return 1
  endif
endif
return 0

[ITEMDEF i_vendor_stock]
ID=i_memory
TYPE=t_eq_script
name=Vendor Stock

ON=@Create
 TIMER=60
 tag(decay,1)
 
on=@unequip
if (tag(linktochar))
	if (tag(linktochar).ischar)
		tag(linktochar).go(<cont.p>)
	endif
endif

on=@timer
if (tag(decay))
  remove
endif
return 1

ON=@TargOn_Char
if (link.isVendorInactive)
  return 0
endif
if (src.targ.ismypet)&&(src.targ.npc)&&(src.targ.iscvendor==0)&&(!src.targ.isconjured)
 tag(linktochar,<src.targ>)
 src.targ.go(500,500, 0, 10)
 src.targ.dialog d_vendor_stock
 tag(decay,"")
else
 timer=60
 Target("Zamer vec ve svem batohu nebo svoje zvire na prodej.")
endif
RETURN 1

ON=@TargOn_Item
if (link.isVendorInactive)
  return 0
endif
if (src.targ.topobj.uid==src.uid)&&(src.targ.cont.uid!=src.uid)
 if (<src.targ.attr>&0b0d4)
  src.sysMessage("Tohle nemuzes prodat.")
 elseif (<src.targ.attr>&attr_stolen)
  src.sysMessage("Tohle jeste nemuzes prodat.")
 elseif (src.targ.type==t_gold)
  src.sysMessage("Nemuzes prodavat hotovost.")
 elseif (src.targ.type==t_container)
  if (<src.targ.rescount> > 500)
   src.sysMessage("V containeru nesmis mit vice jak 500ks zbozi !")
  else
   src.targ.dialog d_vendor_stock
   tag(decay,"")
   return 1
  endif
 else
  src.targ.dialog d_vendor_stock
  tag(decay,"")
  return 1
 endif
 timer=60
 Target
 return 1
else
 timer=60
 Target("Zamer vec ve svem batohu nebo svoje zvire na prodej.")
 RETURN 1
endif
remove
return 1

[ITEMDEF i_vendor_price]
ID=i_memory
TYPE=t_eq_script
name=Vendor price

ON=@Create
 attr=attr_decay
 timer=60

ON=@TargOn_Char
if (link.isVendorInactive)
  return 0
endif
if (src.targ.ismypet)
 f_setprice
else
 timer=60
 target
endif
return 1

ON=@TargOn_Item
if (link.isVendorInactive)
  return 0
endif
if (src.targ.baseID == i_rune_marker)
 target("Nelze vykupovat runy.")
else
 f_setprice
endif
return 1

[function f_setprice]
var(isrealid,0)
link.findID(i_buyer_box).contents(f_isrealid(<src.targ.baseid>))
tag(isrealid,<var.isrealid>)
tag(price,<eval var(isrealid).tag(price)>)
tag(limit,<eval var(isrealid).tag(limit)>)
src.targ.dialog d_vendor_price
TIMER -1

[function f_isrealid]
if (<tag.realid>==<args>)
var(isrealid,<UId>)
else
return 0
endif

[DIALOG d_vendor_price]//input(Nastavit cenu pro <name> (<baseid>),tag.price,<tag.price>)
100,200
argo.tag(sirka,400)
argo.tag(vyska,<eval 23+(3*d_def_radek_vyska)+(4*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1,100)
argo.dialog_pozadi(<argo.tag(nexty)>,1,100)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,0481,Nastavit vykup: <name> (<baseid>))
argo.texta(<argo.dialog_textpos(1,0)>,0481,Cena)
argo.settext(101,<src.findID(i_vendor_price).tag.PRICE>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.textentry(<argo.dialog_textpos(1,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,0,101)

argo.texta(<argo.dialog_textpos(2,0)>,0481,"Limit (ks)")
argo.settext(10,<src.findID(i_vendor_price).tag.limit>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.textentry(<argo.dialog_textpos(2,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,10)

argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel

[DIALOG d_vendor_price TEXT]

[DIALOG d_vendor_price button]
on=0
safe src.findID(i_vendor_price).remove
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

on=1
src.sysMessage("price")
arg(vendor,<src.findID(i_vendor_price).link>)
if (vendor.isVendorInactive)
  return 1
endif
if (src.findID(i_vendor_price).tag(isrealid))
src.act=<src.findID(i_vendor_price).tag.isrealid>
else
src.newitem i_vendor_item
endif
if (ischar)
 src.act.dispID=<typedef.icon>
 if (strlen(<title>))
  src.act.name="CHAR: <?title?>"
 else
  src.act.name="CHAR: <?name?>"
 endif
else
 src.act.dispID=<dispID>
 src.act.name=<finduid(baseid).name>
endif
src.act.color=<color>
src.act.tag(realid,<baseid>)
src.act.tag(price,<fixNumberPositive(<argtxt(0)>)>)
src.act.tag(limit,<fixNumberPositive(<argtxt(1)>)>)
src.act.cont=<arg(vendor).findID(i_buyer_box)>
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")


[DIALOG d_vendor_stock]
100,175
argo.tag(sirka,400)
argo.tag(vyska,<eval (4*d_def_radek_vyska)+(5*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_zpruhledni
if (isitem)
 argo.texta(<argo.dialog_textpos(0,0)>,0481,Na prodej: <amount> <name>)
 argo.settext(10,<qval((amount==1), ,<amount>)><name>)
else
 argo.texta(<argo.dialog_textpos(0,0)>,0481,Na prodej: <name>)
 argo.settext(10,<typedef.name>)
endif
argo.text(<argo.dialog_textpos(0,0)>,0481,100)

argo.settext(110,<?EVAL <src.findID(i_vendor_stock).tag.PRICE>?>)
argo.texta(<argo.dialog_textpos(1,0)>,0481,Popis)
argo.textentry(<argo.dialog_textpos(1,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,0,10)
argo.texta(<argo.dialog_textpos(2,0)>,0481,Cena)
argo.textentry(<argo.dialog_textpos(2,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,110)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,9)//cancel

argo.dialog_textpos(3,0,1)
argo.button(lastxpos,lastypos-1,9905,9904,1,0,3)
argo.textA(lastxpos+30,lastypos,2301,Prodat)
if (iscontainer) && !(ID==i_truhla_heslo)
 argo.button(lastxpos+100,lastypos-1,9905,9904,1,0,1)
 argo.textA(lastxpos+130,lastypos,2301,Jednotkovy prodej)
 argo.button(lastxpos+250,lastypos-1,9905,9904,1,0,2)
 argo.textA(lastxpos+280,lastypos,2301,Nova sekce)
endif

[DIALOG d_vendor_stock TEXT]

[DIALOG d_vendor_stock BUTTON]
on=9
safe src.findID(i_vendor_stock).remove

on=0
safe src.findID(i_vendor_stock).remove
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

on=1//jednotkovy prodej
arg(vendor,<src.findID(i_vendor_stock).link>)
if (vendor.isVendorInactive)
  return 1
endif
if !(<topobj> == <src>)
  src.redMessage("Predmet musis mit u sebe !")
else
  src.newitem i_vendor_item
  src.act.name=<name>
  src.act.color=<color>
  src.act.amount=<amount>
  src.act.link=<UID>
  src.act.tag(price,<fixNumberPositive(<argtxt(1)>)>)
  src.act.cont=<arg(vendor)>
  if (findcont(0))
    src.act.dispID=<findcont(0).dispID>
  else
    src.act.dispID=<dispID>
  endif
  cont=<arg(vendor).topobj.findID(i_vendor_box)>
  src.act.tag(unitdescription,"<argtxt(0)>")
  src.act.tag(description,"<countamount>x <argtxt(0)>")
  src.act.tag(jednot,1)
  safe src.findID(i_vendor_stock).remove
  updatex
endif
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

on=2//nova sekce?
arg(vendor,<src.findID(i_vendor_stock).link>)
if (vendor.isVendorInactive)
  return 1
endif
if !(<topobj> == <src>)
  src.redMessage("Predmet musis mit u sebe !")
else
  tag(description,"<strtoupper(<argtxt(0)>)>")
  name="<argtxt(0)>"
  cont=<arg(vendor)>
  tag(section,1)
  if (rescount)
  dispID=<findcont(0).dispID>
  contents(logbounce)
  endif
  safe src.findID(i_vendor_stock).remove
  updatex
endif
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

on=3//novej item/char
arg(vendor,<src.findID(i_vendor_stock).link>)
if (vendor.isVendorInactive)
  return 1
endif
src.findID(i_vendor_stock).tag(description,"<argtxt(0)>")
src.findID(i_vendor_stock).tag(price,<fixNumberPositive(<argtxt(1)>)>)
if !(strlen(<src.findID(i_vendor_stock).tag.PRICE>))
  src.findID(i_vendor_stock).tag(PRICE,<value>)
endif
if !(strlen(<src.findID(i_vendor_stock).tag.DESCRIPTION>))
  src.findID(i_vendor_stock).tag(DESCRIPTION,"<amount> <name>")
endif
if (ischar)
  if (ismypet)
    src.newitem <typedef.icon>
    src.act.more1=<id>
    src.act.name=<name>
    src.act.color=<color>
    src.act.more2=<uid>
    src.act.tag(alive,1)
    src.act.cont=<arg(vendor).topobj.findID(i_vendor_box)>
    safe src.findID(i_vendor_stock).tag.remove(linktochar)
    arg(figuid,<src.act>)
    src.newitem i_vendor_item
    src.act.name=<name>
    src.act.color=<color>
    src.act.link=<figuid>
    src.act.dispID=<typedef.icon>
    src.act.tag(PRICE,<src.findID(i_vendor_stock).tag.PRICE>)
    src.act.tag(DESCRIPTION,"<src.findID(i_vendor_stock).tag.DESCRIPTION>")
    src.act.cont=<arg(vendor)>
    src.targ.remove
    disconnect
    flags=<flags>|statf_ridden
    skill 70
  else
    src.sysmessage "Toto neni tvoje zvire"
  endif
else
  if !(<topobj> == <src>)
    src.redMessage("Predmet musis mit u sebe !")
  else
    src.newitem i_vendor_item
    src.act.name=<name>
    src.act.color=<color>
    src.act.amount=<amount>
    src.act.link=<UID>
    src.act.tag(PRICE,<src.findID(i_vendor_stock).tag.PRICE>)
    src.act.cont=<src.findID(i_vendor_stock).link>
    src.act.tag(DESCRIPTION,"<src.findID(i_vendor_stock).tag.DESCRIPTION>")
    src.act.dispID=<dispID>
    src.newitem i_pouch
    src.act.name=<amount> <name>
    src.act.color=<color>
    src.act.cont=<src.findID(i_vendor_stock).link.topobj.findID(i_vendor_box)>
    cont=<src.act>
    safe src.findID(i_vendor_stock).remove
    updatex
  endif
endif
safe(<src.tag.cv_lastdialog>)
src.tag(cv_lastdialog,"")

[ITEMDEF i_vendor_box]
ID=i_backpack
name=Kontejner prodavanych veci
LAYER=60

[ITEMDEF i_vendor_tag_box]
ID=i_backpack
name=Hlavni nabidka
LAYER=61

[ITEMDEF i_buyer_box]
ID=i_backpack
name=Seznam vykupovanych veci
LAYER=62

[ITEMDEF i_buyer_tag_box]
ID=i_backpack
name=Docasny seznam veci k prodeji
LAYER=63

on=@userdclick
src.redmessage("Funkce zrusena")
//Target Ktere zvire chces prodat?
return 1

on=@targon_char
arg(vendor,<src.findID(i_buyer_tag_box).link>)
if (vendor.isVendorInactive)
  return 0
endif
if (src.targ.isconjured)
src.sysmessage "Nemuzes prodavat vyvolane nestvury."
elseif !(src.targ.ismypet)
src.sysmessage "Nepatri tobe."
elseif (strlen(<arg(vendor).findID(i_buyer_tag_item).tag(id_<src.targ.baseid>_price)>))
src.targ.dialog d_vendor_sell_item
return 1
else
src.sysmessage "Tohle neni vykupovano."
endif
Target Co chces prodat?
return 1

on=@targon_item
Target Zamer charakter.
return 1

on=@equip
attr=attr_decay
timer=100

[ITEMDEF i_buyer_tag_item]
ID=i_crystal_red
type=t_script
name=Buyer tag item
LAYER=65

[ITEMDEF i_buyer_goods_box]
ID=i_backpack
name=Buyer goods Box
LAYER=64

[ITEMDEF i_vendor_item]
ID=i_memory
TYPE=t_script
name=Vendor Item

[function f_safeitem]
f_safeitem_restore //kdyz si neco vickrat prohlidnu, mohly by se prepsat zalozni attr a tak
events +t_cv_showitem
tag(cv_origcont,<cont>)
tag(cv_origattr,<attr>)
attr_movenever=1

[function f_safeitem_restore]
if (isevent(t_cv_showitem))
 attr=0
 attr=<tag(cv_origattr)>
endif
events -t_cv_showitem
tag.remove(cv_origattr)
tag.remove(cv_origcont)
if (argvcount)
 logcont(<args>)
endif

[typedef t_cv_showitem]
on=@pickup_pack
src.redmessage(Nesahat!)
cont=<tag(cv_origcont)>
return 1

on=@userdclick
if (type==t_container)
 open
 return 1
else
 src.redmessage(Nesahat!)
 cont=<tag(cv_origcont)>
 return 1
endif

on=@stackon
src.redmessage(Nesahat!)
argo.bounce
return 1

on=@equip
src.redmessage(Nesahat!)
cont=<tag(cv_origcont)>
return 1

on=@dropon_obj
src.redmessage(Nesahat!)
cont=<tag(cv_origcont)>
return 1

[DIALOG d_vendor_sell]
SetLocation=10,10
argo.tag(firsti,<eval argv(0)>)//<eval argv(1)>
if (argo.tag(firsti)<0)
 argo.tag(firsti,0)
endif
argo.cv_radku(<rescount>)
argo.tag(sirka,<cv_dialog_sirka>)
argo.tag(vyska,<eval (3*<d_def_radek_vyska>)+(argo.tag(radku)*cv_dialog_radek_vyska)+(4*<d_def_skvira>)+(2*<d_def_okraj>)>)

argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)

var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
var(sloupec_noback[0],1)
argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,20,40,400,80)
var(sloupec_noback[0],"")
var(d_def_radek_vyska,"")
argo.dialog_pozadi(<argo.tag(nexty)>,1)
var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,2301,Co chces prodat? (muzu zaplatit:<?eval (<link.tag.gold>*100)/(100+<link.tag.provize>)?>))
argo.texta(<argo.dialog_textpos(1,0,1)>,2301,kup)
argo.texta(<argo.dialog_textpos(1,2)>,2301,Nazev zbozi)
argo.texta(<argo.dialog_textpos(1,3)>,2301,Cena/gp)
argo.texta(<argo.dialog_textpos(1,4,1)>,2301,Max ks)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,9)//cancel

if (argo.tag(firsti))
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[2])>+1,0fa,0fb,1,0,1) // prev
endif
arg(imax,<argo.tag(firsti)>+<cv_dialog_maxradku>)
if (imax>rescount)
 arg(imax,<rescount>)
else
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[3])>-24,0fc,0fd,1,0,2) // next
endif

arg(ypos,<argo.tag(obj_y[2])>)
arg(index,<argo.tag(firsti)>)
while (index<imax)
 arg(item,<findcont(index)>)
 argo.button(<argo.tag(sloupec_x[0])>,ypos,9905,9904,1,0,index+100)
 argo.tilepic(<argo.tag(sloupec_x[1])>,ypos,<arg(item).dispiddec)>)
 argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,2301,"<arg(item).name>")
 argo.textA(<argo.tag(sloupec_x[3])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(price)>")
 argo.textA(<argo.tag(sloupec_x[4])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(limit)>") 
 arg(ypos,#+<d_def_radek_vyska>)
 arg(index,#+1)
endwhile

 argo.dialog_textpos(3,0,1)
 argo.button(lastxpos,lastypos-1,9905,9904,1,0,3)
 argo.textA(lastxpos+30,lastypos,2301,Prodat zvire)
var(d_def_radek_vyska,"")

[DIALOG d_vendor_sell TEXT]

[DIALOG d_vendor_sell BUTTON]
on=0
remove
on=1//prev
 DIALOG(d_vendor_sell,<argo.tag(firsti)>-cv_dialog_maxradku)
on=2//next
 DIALOG(d_vendor_sell,<argo.tag(firsti)>+<argo.tag(radku)>)
on=3
 dclick 
 
on=@anybutton
arg(vendor,<src.findID(i_buyer_tag_box).link>)
if (vendor.isVendorInactive)
  return 1
endif
arg(radek,<eval argn-100>)
arg(item,<findcont(radek)>)
arg(item).DIALOG d_vendor_sell_item

[DIALOG d_vendor_sell_item]//def: vendoritem v sellboxu
100,200
argo.tag(sirka,400)
argo.tag(vyska,<eval 23+(5*d_def_radek_vyska)+(4*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,3)
argo.dialog_pozadi(<argo.tag(nexty)>,1,80)
argo.dialog_zpruhledni

argo.settext(101,Opravdu to chces prodat?)
argo.text(<argo.dialog_textpos(0,0)>,0481,101)
argo.settext(100,<?qval(safe.amount>1,<safe.amount>,)?> <?name?>)//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.HTMLGUMP(<argo.dialog_textpos(1,0)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),3*d_def_radek_vyska,100,0,1)
argo.texta(<argo.dialog_textpos(2,0)>,0481,Cena)
argo.texta(<argo.dialog_textpos(2,1)>,0481,<?qval(ischar,<src.findID(i_buyer_tag_box).link.findID(i_buyer_tag_item).tag(id_<baseid>_price)>,<safe.tag.PRICE>)?>)
argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel

if (isitem)
	if (type==t_container))
		argo.button(lastxpos+150,lastypos,4005,4007,1,0,2)//
		argo.texta(lastxpos+180,lastypos,0481,Co se prodava?)//
	endif
endif

[DIALOG d_vendor_sell_item TEXT]

[DIALOG d_vendor_sell_item BUTTON]
on=0
src.findID(i_buyer_tag_box).link.vendorspk_sell

on=1
arg(vendor,<src.findID(i_buyer_tag_box).link>)
if (vendor.isVendorInactive)
  return 1
endif
if (ischar)
 src.newitem <typedef.icon>
 src.act.more1=<id>
 src.act.name=<name>
 src.act.color=<color>
 src.act.more2=<uid>
 src.act.link=<uid>
 src.act.tag(alive,1)
 src.act.cont=<src.findID(i_buyer_tag_box)>
 src.act.tag(price,<arg(vendor).findID(i_buyer_tag_item).tag(id_<baseid>_price)>)
 src.act.vendorsell
else
 vendorsell
endif

on=2//projit container
if !(src.isGM)
  src.redMessage("Funkce prozatim zakazana ! Doslo k zneuziti.")
  dialog d_vendor_sell_item
  return 0
endif
arg(vendor,<src.findID(i_buyer_tag_box).link>)
if (vendor.isVendorInactive)
  return 1
endif
if (safe RESCOUNT)
 f_safeitem
 bounce
 open
 if (iscontainer)
  contents2(f_safeitem)
 endif
 cont=<topobj.findID(i_buyer_tag_box)>
else
 src.SYSMESSAGE "Kontejner je prazdny."
endif
dialog d_vendor_sell_item

[function vendorsell]//def: prodavany vendoritem/kontejner v buyer tagboxu (u hrace)
var(tagboxuid,<cont>)
if (type==t_container)
 contents2(f_itemsell)
 if (tag(contprice))
  link.contents(logbounce)//neprodane veci vyhazet
  tag(price,<tag(contprice)>)
  f_itemsell//normalne prodat
 endif
else
 f_itemsell
endif
var(tagboxuid).link.vendorspk_sell

[function f_itemsell]
if (link)
 if (link.isitem)
  if (tag(oamount)!=link.amount)
    src.sysmessage("bugere bugerska - desired amount <tag(oamount)>, supplied <link.amount>")
    src.accmessage("bugere bugerska - desired amount <tag(oamount)>, supplied <link.amount>")
    //src.jail
    return 1
  elseif (link.topobj!=src)
    src.sysmessage("bugere bugerska, tohle neni tvoje")
    return 1
  endif
 endif
else
 //src.jail
 src.sysmessage("bugere bugerska - desired amount <tag(oamount)>, supplied <link.amount>")
 src.accmessage("bugere bugerska - desired amount <tag(oamount)>, supplied <link.amount>")
 remove
 return 1
endif

arg(gold_max,<var(tagboxuid).link.tag(gold)>)
arg(gold_this,<eval (<tag.price>*(100+<var(tagboxuid).link.tag.provize>))/100>)

if (arg(gold_this)<0)
  src.sysmessage("Vysledna cena je mimo funkcni rozsah, zkus prodavat po mensim mnozstvi")
  remove
  return 0
endif

if (gold_max>=gold_this)
 if (link.isitem)
  arg(nextlimit,<eval tag(listuid).tag(limit)-link.amount>)
 else
  arg(nextlimit,<eval tag(listuid).tag(limit)-1>)
 endif
 if (arg(nextlimit)>=0)
  if (link.isitem)
   if (link.type==t_container)
    contents(bounce)
   endif
   link.logcont=<var(tagboxuid).link.findID(i_buyer_goods_box)>
   link.updatex
  else
   logcont=<src.findID(i_buyer_tag_box).link.findID(i_buyer_goods_box)>
   link.disconnect
   link.flags=<link.flags>|statf_ridden
   link.skill 70
   link(0)
  endif
  tag(listuid).tag(limit,<arg(nextlimit)>)
  src.findlayer(21).givegold(<tag.price>)
  var(tagboxuid).link.tag(gold,<eval arg(gold_max)-arg(gold_this)>)
 else
  src.sysmessage Vic kusu <link.name> uz nevykupuji.
 endif
else
 src.sysmessage Nedostatek penez na <name>.
endif
remove

[DIALOG d_vendor_desire]
SetLocation=10,10
argo.tag(firsti,<eval argv(0)>)//<eval argv(1)>
if (argo.tag(firsti)<0)
 argo.tag(firsti,0)
endif
argo.cv_radku(<rescount>)
argo.tag(sirka,<cv_dialog_sirka>)
argo.tag(vyska,<eval (3*<d_def_radek_vyska>)+(argo.tag(radku)*cv_dialog_radek_vyska)+(4*<d_def_skvira>)+(2*<d_def_okraj>)>)

argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)

var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
var(sloupec_noback[0],1)
argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,20,40,400,80)
var(sloupec_noback[0],"")
var(d_def_radek_vyska,"")
argo.dialog_pozadi(<argo.tag(nexty)>,1)
var(d_def_radek_vyska,<cv_dialog_radek_vyska>)
argo.dialog_zpruhledni

arg(hobj,<uid>)
while (<eval arg(hobj).cont>!=<eval arg(hobj).topobj>)
 arg(urovne_uid,"<safe urovne_uid><arg(hobj)>,")
 arg(hobj,<arg(hobj).cont>)
endwhile
arg(urovne_uid,"<safe urovne_uid><arg(hobj)>")

argo.texta(<argo.dialog_textpos(0,0)>,2301,<cm_UIDs_list(<arg(urovne_uid)>)>)
argo.texta(<argo.dialog_textpos(1,0,1)>,2301,kup)
argo.texta(<argo.dialog_textpos(1,2)>,2301,Nazev zbozi)
argo.texta(<argo.dialog_textpos(1,3)>,2301,Cena/gp)
argo.texta(<argo.dialog_textpos(1,4,1)>,2301,Max ks)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,9)//cancel

if (argo.tag(firsti))
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[2])>+1,0fa,0fb,1,0,1) // prev
endif
arg(imax,<argo.tag(firsti)>+<cv_dialog_maxradku>)
if (imax>rescount)
 arg(imax,<rescount>)
else
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+15),<argo.tag(nexty)>-23,0fc,0fd,1,0,2) // next
endif

arg(ypos,<argo.tag(obj_y[2])>)
arg(index,<argo.tag(firsti)>)
while (index<imax)
 if (topobj.ismypet)
  argo.button(<argo.tag(sloupec_x[3])>-35,ypos,0984,0983,1,0,(index*3)+101) 
  argo.button(<argo.tag(sloupec_x[3])>-20,ypos,0984,0984,1,0,(index*3)+102)
  argo.button(<argo.tag(sloupec_x[3])>-20,ypos+8,0983,0983,1,0,(index*3)+102)
  argo.button(<argo.tag(sloupec_x[0])>,ypos,9905,9904,1,0,(index*3)+103)  
 endif
 arg(item,<findcont(index)>)
 argo.tilepic(<argo.tag(sloupec_x[1])>,ypos,<arg(item).dispiddec)>)
 argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,2301,"<arg(item).name>")
 argo.textA(<argo.tag(sloupec_x[3])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(price)>")
 argo.textA(<argo.tag(sloupec_x[4])>+<d_def_odsazeni>,ypos,2301,"<arg(item).tag(limit)>")
 arg(ypos,#+<d_def_radek_vyska>)
 arg(index,#+1)
endwhile

if (topobj.ismypet)//ovladaci tlacitka
 argo.dialog_textpos(3,0,1)
 argo.button(lastxpos,lastypos-1,9905,9904,1,0,3)
 argo.textA(lastxpos+30,lastypos,2301,Pridat vec k vykupu)
 argo.button(lastxpos+400,lastypos-1,9905,9904,1,0,5)
 argo.textA(lastxpos+430,lastypos,2301,Prevratit seznam)
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+46),<argo.tag(nexty)>-23,0a56,0a57,1,0,4) // set all desired max values
else
 argo.texta(<argo.dialog_textpos(3,0)>,2301,Vase konto: <eval src.findlayer(21).rescount(t_gold)+src.bankbalance> gp)
endif
var(d_def_radek_vyska,"")

[DIALOG d_vendor_desire TEXT]

[DIALOG d_vendor_desire BUTTON]
on=0

on=1//prev
DIALOG(d_vendor_desire,<argo.tag(firsti)>-cv_dialog_maxradku)

on=2//next
DIALOG(d_vendor_desire,<argo.tag(firsti)>+<argo.tag(radku)>)

on=3 //add new item of desire
if (isVendorInactive)
  return 1
endif
src.tag.cv_lastdialog="finduid(<?uid?>).dialog d_vendor_desire"
topobj.setprice

on=4 //set all desired max values
dialog(d_vendor_desire_setMaxValues)

on=5 //reverse list
contreverse
dialog d_vendor_desire
 
on=@anybutton
if (isVendorInactive)
  return 1
endif
arg(radek,<eval (argn-101)/3>)
arg(item,<findcont(radek)>)
arg(tlacitko,<eval (argn-100)%3>)
//src.smsg radek cislo <radek> item <arg(item).name> tlacitko <tlacitko>
if (tlacitko==0)
 if (topobj.ismypet)
  arg(item).DIALOG d_vendor_price_2
 endif
elseif (tlacitko==1)
 contmoveup(<radek>)
 DIALOG d_vendor_desire
else
 contmovetop(<radek>)
 DIALOG d_vendor_desire
endif

[DIALOG d_vendor_desire_setMaxValues]
100,175
argo.tag(sirka,400)
argo.tag(vyska,<eval (3*d_def_radek_vyska)+(4*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1,100)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,0481,Upravit vykup: nastaveni vsech max hodnot)

argo.texta(<argo.dialog_textpos(1,0)>,0481,Limit (ks))
argo.textentry(<argo.dialog_textpos(1,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,110)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,0)//cancel

argo.dialog_textpos(2,0,1)
argo.button(lastxpos,lastypos-1,9905,9904,1,0,1)
argo.textA(lastxpos+30,lastypos,2301,Nastavit)

[DIALOG d_vendor_desire_setMaxValues BUTTON]
on=0
dialog(d_vendor_desire)

on=1 //Nastavit
if (topobj.ismypet)
  arg(imax,<rescount>)
  arg(index,0)
  while (index<imax)
    arg(item,<findcont(index)>)
    arg(item).tag(limit,<fixNumberPositive(<argtxt(1)>)>)
    arg(index,#+1)
  endwhile
endif
dialog(d_vendor_desire)

[function pricereset]
if (topobj.ismypet)
dialog d_vendor_price_2
else
topobj.findID(i_buyer_box).dialog d_vendor_desire
endif

[DIALOG d_vendor_price_2]
100,175
argo.tag(sirka,400)
argo.tag(vyska,<eval (4*d_def_radek_vyska)+(5*d_def_skvira)+(2*d_def_okraj)>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1,100)
argo.dialog_pozadi(<argo.tag(nexty)>,1,100)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_zpruhledni

argo.texta(<argo.dialog_textpos(0,0)>,0481,Upravit vykup: <?name?>, (<?tag.realid?>))


argo.text(<argo.dialog_textpos(0,0)>,0481,100)


//(d_Def_odsazeni(2*(d_def_okraj+d_def_skvira)))
argo.settext(10,<tag.PRICE>)
argo.texta(<argo.dialog_textpos(1,0)>,0481,Cena)
argo.textentry(<argo.dialog_textpos(1,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,0,10)
argo.settext(110,<tag.limit>)
argo.texta(<argo.dialog_textpos(2,0)>,0481,Limit (ks))
argo.textentry(<argo.dialog_textpos(2,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,110)
//argo.texta(<argo.dialog_textpos(2,0)>,0481,Cena)
//argo.textentry(<argo.dialog_textpos(2,1)>,argo.tag(sirka)-(d_Def_odsazeni+(2*(d_def_okraj+d_def_skvira))),d_def_radek_vyska,0481,1,110)

//30 60 350 20 4 0 2
//argo.button(<d_def_okraj>,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb7,0fb9,1,0,1)//ok
//argo.button(<d_def_okraj>+33,(<argo.tag(vyska)>-<d_def_okraj>)-23,0fb1,0fb3,1,0,0)//cancel
button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,9)//cancel

argo.dialog_textpos(3,0,1)
argo.button(lastxpos,lastypos-1,9905,9904,1,0,3)
argo.textA(lastxpos+30,lastypos,2301,Nastavit)
argo.button(lastxpos+100,lastypos-1,9905,9904,1,0,2)
argo.textA(lastxpos+130,lastypos,2301,Odebrat)

[DIALOG d_vendor_price_2 TEXT]
//<?name?>, (<?tag.realid?>)
//Price
//<?EVAL <tag.PRICE>?>
//remove

[DIALOG d_vendor_price_2 button]
on=0
topobj.findID(i_buyer_box).dialog d_vendor_desire
on=2//odebrat
if (isVendorInactive)
  return 1
endif
src.act=<topobj.findID(i_buyer_box)>
remove
src.act.dialog d_vendor_desire
on=3//ok
if (isVendorInactive)
  return 1
endif
tag(price,<fixNumberPositive(<argtxt(0)>)>)
tag(limit,<fixNumberPositive(<argtxt(1)>)>)
//src.sysmessage "<name>: cena nastavena na <eval <tag.PRICE>> zlata."
topobj.findID(i_buyer_box).dialog d_vendor_desire

[itemdef i_infobook]
id=i_spellbook
type=t_book
name=Vendor Help
on=@create
more1=b_vendor_navod_1

[BOOK b_vendor_navod_1]
PAGES=20
TITLE=Info pro zakaznika vendora
AUTHOR=Moria

[BOOK b_vendor_navod_1 1]
Na dalsich stranach 
jsou popsany prikazy 
kterymy muzete jako 
zakaznik prodavat ci 
nakupovat u hrac-
skeho vendora
Zadavejte ve tvaru 
"<jmeno> <prikaz>"

[BOOK b_vendor_navod_1 2]
"NAKUP","KUP",
"SUPPLIES","WARES"
nabidka veci ktere 
ma vendor na prodej
text u jednotliveho 
zbozi na teto strance 
nemusi odpovidat 
skutecnemu nazvu ci 

[BOOK b_vendor_navod_1 3]
mnozstvi veci.
Na potvrzovacim dialogu 
v zahlavi se doctete 
skutecne mnozstvi 
a jmeno prodavane veci
V pripade kontejneru 
muzete zkontrolovat 
jejich obsah 

[BOOK b_vendor_navod_1 4]
prislusnym tlacitkem,
stejne jako u 
jednotkoveho prodeje, 
kde si muzte nastavit
primo do dialogu
kolik veci z kontej-
neru kupujete

[BOOK b_vendor_navod_1 5]
"POPTAV","DESIRE"
seznam veci vykupo-
vanych vendorem 
i s jejich cenami
"KDO","KOMU"
Informuje vas,
kdo je zamestnava-
telem vendora






[BOOK b_vendor_navod_2]
PAGES=20
TITLE=Info pro majitele vendora
AUTHOR=Moria

[BOOK b_vendor_navod_2 1]
PRODEJ:
"STOCK","SKLAD"
kurzorem ukazte na 
vec ve vasem inventorari
(nebo zvire ve vasem 
vlastnictvi), 
urcene k prodeji.
V dalsi tabulce nasta-

[BOOK b_vendor_navod_2 2]
vite jeji popis a cenu 
vepsanim do formulare.
Pokud chcete prodavat
vetsi mnozstvi veci 
za jednotnou cenu,
dejte je do kontejneru,
ukazte na nej kurzo-
rem a zaskrtnete

[BOOK b_vendor_navod_2 3]
"Pouzit jako jednotkovy 
kontejner" Cena kterou 
zadate je pocitana za 
jedbu kazdou vec 
v kontejneru.

[BOOK b_vendor_navod_2 4]
'Nakup','Buy',
'Supplies','Wares'
V tomto menu muzete vami 
prodavane veci stahovat 
z prodeje, v pripade 
jednotkoveho kontejneru 
muzete do nej pridavat 
veci pouzitim prislus-

[BOOK b_vendor_navod_2 5]
neho tlacitka v podmenu 
a ukazanim na dalsi 
kontejner, jehoz obsah 
bude presunut do kontej-
neru v prodeji.

[BOOK b_vendor_navod_2 6]
VYKUP :
"CENA","PRICE",
"NASTAVIT"
kurzorem ukazte na 
vec kterou chcete 
aby vendor vykupoval, 
cenu nastavite
v dalsim dialogu. 

[BOOK b_vendor_navod_2 7]
"DESIRE","POPTAV"
kromne seznamu vy-
kupovaneho zbozi 
slouzi tento dialog 
i k editaci ceny, 
pripadne preruseni 
vykupu dane veci

[BOOK b_vendor_navod_2 8]
"GOLD","PENEZ",
"PENIZE"
Informuje vas o stavu 
vendorova konta.
"PRIDAT","ADD"
Vybranim hromadky minci 
pridate vendorovi 
penize na vykup.

[BOOK b_vendor_navod_2 9]
"OBLECT","OBLECENI",
"CLOTHING"
prikaz pro obleceni 
vendora. Vymeni to 
co mate na sobe vy 
za to co ma na sobe 
vendor, a to i s 
osedlanym zviretem.

[BOOK b_vendor_navod_2 10]
"COLLECT","VYDELEK"
to nejlepsi nakonec-
timto prikazem vam
vendor vyda nakou-
pene zbozi (do banky)
a utrzene penize 
(do batohu).



[eof]                  