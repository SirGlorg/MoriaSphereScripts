[function clients]
admin(<args>)

[function spocitejmordoryagondory]
var(pocethracuvrealmu[-2],0)
var(pocethracuvrealmu[-1],0)
var(pocethracuvrealmu[1],0)
var(pocethracuvrealmu[2],0)
var(pocethracuvtutorialu,0)
serv.ALLCLIENTS(f_spocitejmordoryagnodory)
sysmessage("Mordorskych vyvrhelu: <var(pocethracuvrealmu[-2])>")
sysmessage("Gondorskych vyvrhelu: <var(pocethracuvrealmu[-1])>")
sysmessage("Gondoru: <var(pocethracuvrealmu[1])>")
sysmessage("Mordoru: <var(pocethracuvrealmu[2])>")
sysmessage("Nbiku v tutorialu: <var(pocethracuvtutorialu)>")

[function f_spocitejmordoryagnodory]
if !(isGM)
  if (strcmpi(<region.name>,"tutorial"))
    var(pocethracuvrealmu[<tag.realm>],#+1)
  else
    var(pocethracuvtutorialu,#+1)
  endif
endif

[function admin]
if (src.isplayer)
 if (<argv(0)>)
  arg(sortby,<argv(0)>)
 else
  arg(sortby,Name)
 endif
 dialog d_admin <arg(sortby)>,<eval argv(1)>
endif
spocitejmordoryagondory

[DIALOG d_admin]
 //argo.ContextEnum(0,046)
 argo.tag(sortby,<?argv(0)?>)
 argo.PlayerList(<?argo.tag.sortby?>)
SetLocation=0,0				// ok*

argo.tag(sirka,<d_def_admin_sirka>)
argo.tag(vyska,<d_def_admin_vyska>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.tag(radku,<eval((<argo.tag(vyska)>-(<argo.tag(nexty)>+<d_def_okraj>+<d_def_skvira>))/<d_def_radek_vyska>)>)
argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,29,29,170,29,170,29,170)
argo.dialog_zpruhledni

texta(<argo.dialog_textpos(0,0)>,2301,ADMIN dialog - seznam pripojenych klientu  - celkem: <serv.statclients>)
texta(<argo.dialog_textpos(1,0,1)>,2301,come)
textA(<argo.dialog_textpos(1,2)>,2301,Account name)
textA(<argo.dialog_textpos(1,4)>,2301,Char name)
textA(<argo.dialog_textpos(1,6)>,2301,Location name)
textA(<argo.dialog_textpos(1,7)>,2301,Action)

argo.button(<argo.dialog_textpos(1,1,1)>,4005,4007,1,0,1)
argo.button(<argo.dialog_textpos(1,3,1)>,4005,4007,1,0,2)
argo.button(<argo.dialog_textpos(1,5,1)>,4005,4007,1,0,3)

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,4)//cancel
//
argo.tag(firsti,<eval argv(1)>)
if (argo.tag(firsti))
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(obj_y[2])>+1,0fa,0fb,1,0,5) // prev
endif
arg(imax,<argv(1)>+<argo.tag(radku)>)
if (imax>argo.contextcount)
 arg(imax,<argo.contextcount>)
else
 argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>+16),<argo.tag(nexty)>-24,0fc,0fd,1,0,6) // next
endif

arg(ypos,<argo.tag(obj_y[2])>)
arg(index,<argo.tag(firsti)>)
while (index<imax)
 arg(pClient,<?argo.ContextEnum(index)?>)
 arg(pcolor,<arg(pClient).getNameHue>)
 argo.button(<argo.tag(sloupec_x[0])>,ypos,4005,4007,1,0,(4*index)+10)//come
 argo.button(<argo.tag(sloupec_x[1])>,ypos,4005,4007,1,0,(4*index)+12)//accountinfo
 argo.button(<argo.tag(sloupec_x[3])>,ypos,4005,4007,1,0,(4*index)+13)//charinfo
 argo.button(<argo.tag(sloupec_x[5])>,ypos,4005,4007,1,0,(4*index)+11)//goto
 
 argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).AccountName?>") 
 argo.textA(<argo.tag(sloupec_x[4])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).f_adname?>")
 argo.textA(<argo.tag(sloupec_x[6])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).region.name?>")
 argo.textA(<argo.tag(sloupec_x[7])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).myaction?>")
 arg(ypos,#+<d_def_radek_vyska>)
 arg(index,#+1)
endwhile

[DIALOG d_admin BUTTON]
ON=0
	return
on=1
DIALOG(d_admin,Location,<argo.tag(firsti)>)
on=2
DIALOG(d_admin,Account,<argo.tag(firsti)>)
on=3
DIALOG(d_admin,Name,<argo.tag(firsti)>)
on=4
return
on=5//prev
DIALOG(d_admin,<argo.tag(sortby)>,<argo.tag(firsti)>-<argo.tag(radku)>)
on=6//next
DIALOG(d_admin,<argo.tag(sortby)>,<argo.tag(firsti)>+<argo.tag(radku)>)

ON=@AnyButton 

arg(radek,<eval (argn-10)/4>)
arg(pClient,<argo.ContextEnum(radek)>)

doswitch <eval argn-(10+(<radek>*4))>
 arg(pclient).try summonto
 arg(pclient).try srccome
 arg(pclient).try accountinfo
 arg(pclient).try tweak
enddo

[function f_adname]
if (account.plevel>1)&&(strcmpi(<name>,Eufegenie))
 if (isinvis)
  return"*<name>"
 else
  return"+<name>"
 endif
else
 return"<name>"
endif

[dialog d_admin_ip]
argo.tag(sortby,<?argv(0)?>)
argo.PlayerList(<?argo.tag.sortby?>)

SetLocation=0,0

argo.tag(sirka,<d_def_admin_sirka>)
argo.tag(vyska,<d_def_admin_vyska>)
argo.dialog_prvni
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.dialog_pozadi(<argo.tag(nexty)>,1)
argo.tag(radku,<eval((<argo.tag(vyska)>-(<argo.tag(nexty)>+<d_def_okraj>+<d_def_skvira>))/<d_def_radek_vyska>)>)
argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,29,29,110,29,139,110,110,29,139,29)
argo.dialog_zpruhledni

texta(<argo.dialog_textpos(0,0)>,2301,"ADMIN IP dialog - seznam pripojenych klientu a jejich IP")
textA(<argo.dialog_textpos(1,0)>,2301,"come")
textA(<argo.dialog_textpos(1,2)>,2301,"Account name")
textA(<argo.dialog_textpos(1,4)>,2301,"Char name")
textA(<argo.dialog_textpos(1,5)>,2301,"First IP")
textA(<argo.dialog_textpos(1,6)>,2301,"Last IP")
textA(<argo.dialog_textpos(1,7)>,2301,"goto")
textA(<argo.dialog_textpos(1,8)>,2301,"Location")
textA(<argo.dialog_textpos(1,9)>,2301,"accs")

button((<argo.tag(sirka)>-<d_def_okraj>)-33,<argo.tag(obj_y[0])>,0fb1,0fb3,1,0,1)//cancel
//
argo.tag(firsti,<eval argv(1)>)
if (argo.tag(firsti))
  argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>-5),<argo.tag(obj_y[2])>+1,0fa,0fb,1,0,2) // prev
endif
arg(imax,<argv(1)>+<argo.tag(radku)>)
if (imax>argo.contextcount)
  arg(imax,<argo.contextcount>)
else
  argo.button(<argo.tag(sirka)>-(<d_def_okraj>+<d_def_skvira>-5),<argo.tag(nexty)>-24,0fc,0fd,1,0,3) // next
endif

arg(ypos,<argo.tag(obj_y[2])>)
arg(index,<argo.tag(firsti)>)

if (<eval argv(2)> == 0)
  while (index<imax)
    arg(pClient,<?argo.ContextEnum(index)?>)
    arg(pcolor,<arg(pClient).getNameHue>)
  
    argo.button(<argo.tag(sloupec_x[0])>,ypos,4005,4007,1,0,(5*index)+10)//come
    argo.button(<argo.tag(sloupec_x[1])>,ypos,4005,4007,1,0,(5*index)+11)//accountinfo
    argo.button(<argo.tag(sloupec_x[3])>,ypos,4005,4007,1,0,(5*index)+12)//charinfo
    argo.button(<argo.tag(sloupec_x[7])>,ypos,4005,4007,1,0,(5*index)+13)//goto
    argo.button(<argo.tag(sloupec_x[9])>,ypos,4005,4007,1,0,(5*index)+14)//accs
   
    argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).AccountName?>") 
    argo.textA(<argo.tag(sloupec_x[4])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).f_adname?>")
    argo.textA(<argo.tag(sloupec_x[5])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).Account.firstip?>")
    argo.textA(<argo.tag(sloupec_x[6])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).Account.lastip?>")
    argo.textA(<argo.tag(sloupec_x[8])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).region.name?>")
    arg(ypos,#+<d_def_radek_vyska>)
    arg(index,#+1)
  endwhile
else
  while (index<<serv.statclients>)
    arg(pClient,<?argo.ContextEnum(index)?>)
    arg(pcolor,<arg(pClient).getNameHue>)
  
    if (<arg(pClient).Account.lastip> == <argv(2)>)
      argo.button(<argo.tag(sloupec_x[0])>,ypos,4005,4007,1,0,(5*index)+10)//come
      argo.button(<argo.tag(sloupec_x[1])>,ypos,4005,4007,1,0,(5*index)+11)//accountinfo
      argo.button(<argo.tag(sloupec_x[3])>,ypos,4005,4007,1,0,(5*index)+12)//charinfo
      argo.button(<argo.tag(sloupec_x[7])>,ypos,4005,4007,1,0,(5*index)+13)//goto
      argo.button(<argo.tag(sloupec_x[9])>,ypos,4005,4007,1,0,(5*index)+14)//accs
       
      argo.textA(<argo.tag(sloupec_x[2])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).AccountName?>") 
      argo.textA(<argo.tag(sloupec_x[4])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).f_adname?>")
      argo.textA(<argo.tag(sloupec_x[5])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).Account.firstip?>")
      argo.textA(<argo.tag(sloupec_x[6])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).Account.lastip?>")
      argo.textA(<argo.tag(sloupec_x[8])>+<d_def_odsazeni>,ypos,pcolor,"<?arg(pClient).region.name?>")
      arg(ypos,#+<d_def_radek_vyska>)
    endif
     arg(index,#+1)
  endwhile
endif

[DIALOG d_admin_ip BUTTON]
ON=0
return
on=1
return
on=2//prev
DIALOG(d_admin_ip,<argo.tag(sortby)>,<argo.tag(firsti)>-<argo.tag(radku)>)
on=3//next
DIALOG(d_admin_ip,<argo.tag(sortby)>,<argo.tag(firsti)>+<argo.tag(radku)>)

ON=@AnyButton 

arg(radek,<eval (argn-10)/5>)
arg(pClient,<argo.ContextEnum(radek)>)

//arg(pclient).try accip(<arg(pClient).Account.lastip>)
doswitch <eval argn-(10+(<radek>*5))>
  arg(pclient).try summonto
  arg(pclient).try accountinfo
  arg(pclient).try tweak
  arg(pclient).try srccome
  arg(pclient).try accip_online("<?arg(pClient).Account.lastip?>")
enddo

[function accip_online]
if (src.isgm)
  arg(sortby,Name)
  dialog(d_admin_ip,<arg(sortby)>,<eval argv(1)>,<args>)
endif

[function admin_ip]
if (src.isgm)
  arg(sortby,Name)
  dialog(d_admin_ip,<arg(sortby)>,<eval argv(1)>)
endif
return(1)

[eof]