[itemdef i_discordance_timer]
ID=i_memory
TYPE=t_eq_script
name=discordance

on=@equip
if(<link.isitem>)
 cont.sysmessage(Zameruj jen monstra)
 remove
 return 1
endif
if (<link.npc>==0)||(<link.enticement>)
 cont.sysmessage Tohle nemuzes oslabit.
 remove
 return 1
endif
if (<eval(promilerange(<eval(<cont.enticement>)>,<Skill_Enticement.effect>))> < <link.tag(experience)>)
 cont.sysmessage(Oslabeni teto prisery presahuje tve moznosti)
 remove
 return 1
endif
cont.reveal
if (<cont.musicianship> <{0 1025})
 morex=0
 cont.sysmessage(You play poorly.)
 cont.sound <cont.findtype(t_musical).typedef.tdata2>
else
  arg(power,<eval(randfakt(<eval(<cont.enticement>)>,<Skill_Enticement.effect>))>)
  if (<arg(power)> < <link.tag(experience)>)
   morex=0
   cont.sound <cont.findtype(t_musical).typedef.tdata1>
  else
   morex=1
   cont.sound <cont.findtype(t_musical).typedef.tdata1>
  endif
endif
//timerdelay(<promilerange(<cont.enticement>,<skill_enticement.delay>)>)
timerd=(<promilerange(<cont.enticement>,<skill_enticement.delay>)>)
more1=1

on=@timer
if (more1)
 if (morex)
  cont.classmessage(Oslabeni uspesne.)
  if (link.findid(i_rune_discordance))
   link.findid(i_rune_discordance).remove
  endif
  link.tag(discorded,<eval cont.enticement/4>)
  link.newitemsafe(i_rune_discordance)
  link.equip(<lastnew>)
  lastnew.timer=<eval 10+(<cont.enticement>/20)>
 else
  cont.redmessage(Oslabeni se nepovedlo.)
//  link.attack(<cont>)
  link.findid(i_rune_discordance).remove
 endif
endif
remove
return 1

[itemdef i_rune_discordance]
ID=i_memory
TYPE=t_eq_script
name=discorded

on=@equip
f_discordlower(dex)
f_discordlower(str)
f_discordlower(int)
arg(orig_maxhits,<cont.maxhits>)
if (<cont.hits> > <cont.maxhits>)
  cont.maxhits = <cont.hits>
endif
f_discordlower(maxhits)
f_discordlower(maxmana)
f_discordlower(maxstam)
f_discordlower(magery)
f_discordlower(ei)
f_discordlower(resist)
f_discordlower(wrestling)
f_discordlower(archery)
f_discordlower(macefighting)
f_discordlower(fencing)
f_discordlower(swordsmanship)
f_discordlower(poisoning)
if (cont.hits>cont.maxhits)
 cont.hits=<cont.maxhits>
endif
if (<orig_maxhits> < <cont.maxhits>)
 cont.maxhits=<orig_maxhits>
endif
if (cont.mana>cont.maxmana)
 cont.mana=<cont.maxmana>
endif
if (cont.stamina>cont.maxstam)
 cont.stamina=<cont.maxstam>
endif

on=@unequip
f_discordreturn(dex)
f_discordreturn(str)
f_discordreturn(int)
f_discordreturn(maxhits)
f_discordreturn(maxmana)
f_discordreturn(maxstam)
f_discordreturn(magery)
f_discordreturn(ei)
f_discordreturn(resist)
f_discordreturn(wrestling)
f_discordreturn(archery)
f_discordreturn(macefighting)
f_discordreturn(fencing)
f_discordreturn(swordsmanship)
f_discordreturn(poisoning)
cont.tag(discorded,"")

on=@timer
remove
return 1

[function f_discordlower]
arg(lower,<eval (cont.<args>*cont.tag(discorded))/1000>)
if (cont.<args><arg(lower))
 arg(lower,<?cont.<args>?>)
endif
cont.<args>=<eval cont.<args>-arg(lower)>
tag(lowed<args>,<arg(lower)>)

[function f_discordreturn]
cont.<args>=<eval cont.<args>+tag(lowed<args>)>

[function timerdelay]
newitemSafeChar(i_timerd_univ)
//cont.newitemsafe(i_timerd_univ)
lastnew.attr=attr_invis
lastnew.link=<uid>
lastnew.p=<topobj.p>
lastnew.removefromview
lastnew.timerd=<args>

[itemdef i_timerd_univ]
id=i_gold
name=timerd univ

on=@timer
link.trigger(timer)
remove
return 1


[eof]
