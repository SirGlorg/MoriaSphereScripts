[function findvendorplac]
  //if(uid==040000047)
  //src.classmessage(nalezl jsem ten worldgem na zemi vedle)
  //endif
if (isvendorplac)
 //src.classmessage(nalezl jsem nejakej vendorplac)
 if (srcissameregion(<args>))
  var(vendorplacfound,<uid>)
  //return 1
 endif
endif


[function findvendor]
//src.sysmessage(hledam vendora)
if (isvendor) 
 if (issameregion(<args>))
  var(vendorfound,<uid>)
  src.sysmessage(vendor nalezen, uid je <var(vendorfound)>, jmeno <finduid(<var(vendorfound)>).name>)
 endif
endif

[function isvendor]
if (npc)  
  if ((obody==c_h_vendor)||(obody==c_h_vendor_f))
    //src.sysmessage(nasel jsem npc, jeho id je <uid>, jmeno <name>, telo je <obody>)
    return 1
  endif
endif
return 0

[function isvendorplac]
//if (findres(type,<src.type>)==NID(t_vendorplac))  
//if (type==<NID(t_vendorplac)>)
if (type==t_vendorplac)
 return 1
endif
return 0

[function f_vend_ownershipto]//uid of future owner, needs src
more1=<args> //novy majitel
if (safe tag.vend_ownermemUID)
 if (finduid(<tag.vend_ownermemUID>).isitem)
  src.act=<tag.vend_ownermemUID>
 else
  src.newitem i_key_gold
 endif
else
 src.newitem i_key_gold
endif
src.act.cont=<finduid(<args>).findlayer(102)>
src.act.more1=<uid> //
src.act.more2=1 //0- verejnost, 1 - majitel, 2-GM 
src.act.link=<uid> //linkovat ten klic na vendorplac (to je to <uid>)
src.act.name=owner:<name>
src.act.updatex
tag(vend_ownermemUID,<lastnew>)

[function f_eq_vmsignlayer]
if !(findlayer(102))
 newitem i_memory_vendorsign
 equip(act)
endif


[function f_checkvendornajem]
arg(foundGoldID,<tag(najemcont).findtype(t_gold)>)
while (arg(foundGoldID)) && !(tag(paiduntil) == 2147483600) //25920000 - decisekund za mesic (30 dni) //2147483600 - maximalni rozlisitelnost v signed 32Integeru
  arg(pridatcasu,<eval ((arg(foundGoldID).amount*2592)/tag(najem))*10000>)
  if (<eval (2147483600-tag(paiduntil))> < arg(pridatCasu) ) // doslo by prictenim celeho casu k preteceni ?
    tag(paiduntil,2147483600)
  else
    tag(paiduntil,<eval tag(paiduntil)+arg(pridatcasu)>)
  endif
  arg(foundGoldID).remove
  arg(foundGoldID,<tag(najemcont).findtype(t_gold)>)
endwhile

f_payRentFrom(i_cashiers_check)
f_payRentFrom(i_cashiers_mesec)
f_payRentFrom(i_cashiers_pokladnicka)

if (tag(autorent))
  if (<eval(tag(paiduntil))> < <eval(serv.time)>)
    src.redmessage(Nedostatecne zaplaceny najem - vystehovavam prostor pro vendora.)
    trashvendorplac
    trashvendor_itagy
  endif
endif
if (tag(paiduntil) == 2147483600)
  src.sysMessage("Najem je plne zaplacen a nemuze byt dale navysovan.")
endif

[function f_payRentFrom] //argv(0) - id of an object, which <more> holds the info for paying
// 25 920 000 decisekund za 30 dni
// 864 000 decisekund za 1 den
// 36 000 decisekund za 1 hod
// 600 decisekund za 1 min
arg(maxPayTime,<eval 2147483600-<serv.time>>)

arg(foundID,<tag(najemcont).findID(<argv(0)>)>)
arg(dailyPay,<eval tag(najem)/30>)
arg(hourPay,<eval tag(najem)/720>)
arg(minutePay,<eval tag(najem)/43200>)
arg(toPay,<arg(foundID).more>)
arg(timeToAdd,0)
if (<maxPayTime> < <dailyPay>)
  return 1
endif
while (arg(foundID)) //25920000 - decisekund za mesic (30 dni) //2147483600 - maximalni rozlisitelnost v signed 32Integeru
  if (<arg(toPay)> > <arg(dailyPay)>)
    arg(monthsToAdd,<arg(toPay)>/<eval tag(najem)>)
    if (<arg(monthsToAdd)> > 80) //max 82 months can be payed
      arg(timeToAdd,2073600000)	//80 months Time
      if (2073600000 > <arg(maxPayTime)>)	//we can't pay more than 80 months (value on the left is 80 months in ds)
        arg(timeToAdd,<arg(maxPayTime)>)
      endif
      arg(hoursToPay,<arg(timeToAdd)>/36000)	//number of hours to pay
      arg(timeToAdd,<arg(hoursToPay)>*36000)
      if (<eval (2147483600-tag(paiduntil))> <= arg(timeToAdd) ) // doslo by prictenim celeho casu k preteceni ?
        src.redMessage("Najem je preplacen ! V truhle je jeste sek na <arg(foundID).more>gp.")
        return 1
      endif
      arg(foundID).more = <arg(foundID).more> - <eval arg(hoursToPay)*arg(hourPay)>
      tag(paiduntil,<eval tag(paiduntil)+arg(timeToAdd)>)
      return 1
    endif
    arg(timeToAdd,#+<eval arg(monthsToAdd)*25920000>)
    arg(toPay,#-<eval arg(monthsToAdd)*<eval tag(najem)>>)
  endif
  if (<arg(toPay)> > <arg(dailyPay)>)
    arg(daysToAdd,<arg(toPay)>/<arg(dailyPay)>)
    arg(timeToAdd,#+<eval arg(daysToAdd)*864000>)
    arg(toPay,#-<eval arg(daysToAdd)*arg(dailyPay)>)
  endif
  if (<arg(toPay)> > <arg(hourPay)>)
    arg(hoursToAdd,<arg(toPay)>/<arg(hourPay)>)
    arg(timeToAdd,#+<eval arg(hoursToAdd)*36 000>)
    arg(toPay,#-<eval arg(hoursToAdd)*arg(hourPay)>)
  endif
  if (<arg(toPay)> > <arg(minutePay)>)
    arg(minutesToAdd,<arg(toPay)>/<arg(minutePay)>)
    arg(timeToAdd,#+<eval arg(minutesToAdd)*600>)
    arg(toPay,#-<eval arg(minutesToAdd)*arg(minutePay)>)
  endif
  if (arg(timeToAdd) < 0)	//pro jistotu
    src.redMessage("vyskytla se chyba a pri vypoctu najmu doslo k preteceni ! Kontaktuj GM")
    return 1
  endif
  if (<eval (2147483600-tag(paiduntil))> <= arg(timeToAdd) ) // doslo by prictenim celeho casu k preteceni ?
    src.redMessage("Najem je preplacen ! V truhle je jeste sek na <arg(foundID).more>gp.")
    return 1
  endif
  tag(paiduntil,<eval tag(paiduntil)+arg(timeToAdd)>)
  arg(foundID).remove
  arg(foundID,<tag(najemcont).findID(i_cashiers_check)>)
endwhile
return 0

[function trashvendor] //premisteni vendora bez smazani tagu (nelze pridat noveho)
//src.sysmessage(trashing vendor)
var(vendorfound,<tag(vendoruid)>)
//sector.allchars(findvendor(<uid>)) //melo by nalezt vendora v sektoru v nemz lezi item s timto UID
 if(var(vendorfound)) //a vratit vendorovo UID
  arg(kamvendora_x,<eval {5917 5922}>)  //vybrani nahodne pozice v rohu mistnosti s truhlama
  arg(kamvendora_y,<eval {94 99}>)
  finduid(var(vendorfound)).go(<arg(kamvendora_x)>,<arg(kamvendora_y)>,0,10) //presun vendora (mistnost s truhlama, 10 mapplan)
  tag(vendor_trest,1)
  return 1  
 else 
  if(<src.isgm>) //ostatni to zajimat nemusi   
   src.sysmessage(Vendor nenalezen!) 
  endif
 endif
return 0

[function trashvendor_itagy]
//src.sysmessage(trashing vendor)
var(vendorfound,<tag(vendoruid)>)
//sector.allchars(findvendor(<uid>)) //melo by nalezt vendora v sektoru v nemz lezi item s timto UID
 if(var(vendorfound)) //a vratit vendorovo UID
  arg(kamvendora_x,<eval {5917 5922}>)  //vybrani nahodne pozice v rohu mistnosti s truhlama
  arg(kamvendora_y,<eval {94 99}>)
  finduid(var(vendorfound)).go(<arg(kamvendora_x)>,<arg(kamvendora_y)>,0,10) //presun vendora (mistnost s truhlama, 10 mapplan)
  tag(vendpos_nastavena,0) 
  tag(vendorplaced,0)
  tag.remove(vendpos)
  tag.remove(vendoruid) 
  tag.remove(vendor_trest)
  return 1  
 else
  if(<src.isgm>)
   src.sysmessage(Vendor nenalezen!) 
  endif
 endif
return 0

[function trashvendorplac]
if !(src) 
 src=<more1> 
endif
src.accmsg(trashes vendorplac uid=<uid> (<name>))
if (<src.ischar>) 
 src.newitemsafe(i_backpack)
 tag(backpack_s_vecma,<lastnew>)
 lastnew.name="veci ze stanku <?name?> - <?serv.rtimetext?>"
 //lastnew.attr=<eval lastnew.attr|attr_move_never|attr_invis>
 lastnew.attr_invis=1
 lastnew.attr_movenever=1
 foreachlocked(trashvenditem)
 array_clear(tag.lockedlist)
 if (finduid(tag(backpack_s_vecma)).rescount)
  finduid(tag(backpack_s_vecma)).cont=<tag(najemcont)>  
 else
  finduid(tag(backpack_s_vecma)).remove
 endif
 tag.remove(backpack_s_vecma)
 //link.tag.remove(mammajitele) //plac skonci bez majitele, ze...
 tag(ownerscont).contents(link.remove)
 tag(ownerscont).emptycont
 tag.remove(bankactivated)
 //finduid(<tag(vend_ownermemUID)>).remove
 if !(<tag(drazba)>) //bezi li drazba, bude nadale pokracovat a nic se nedeje
  //src.sysmessage(neni drazba, nuluju)
  tag(najem,50000) //nova drazba zacne automaticky na 50k  
 endif 
 more1=094dfd //majitelem se stane Dinivan :o)  - pro drazbu a aby si nikdo nemohl jen tak vzit stanek
endif

[function trashvenditem]
if ((isevent(t_vendormenu))||(type==t_vendormenu))
  //vyvesky nechame viset :)
  return
else
  attr=<tag(vm_storeattr)>
  tag(vm_storeattr,"")
  tag(vm_lockedto,"")
  tag(vm_lockit,"")
  unblockitem
  events -t_lockeditem
  //to s tema dverma tam nechavam, nicemu to neskodi a co kdyby tam mel vystaveny dvere :)) 
  if ((type==t_door_locked)||(type==t_door))
   type=t_door
   link(0)
  else
   removefromview
   src.accmsg(trashes item uid=<uid> (<amount> <name>))
   cont=<lastnew>
  endif
endif

[function f_vendormenu]
link.tag(lastusedsign,<src.uid>) //link v této funkci je link té cedule t_vendormenu - coz je samozrejme ten dum/stanek
src.f_eq_vmsignlayer

//nasledujici blok se pouzije v pripade, ze uz existuje v dane oblasti vendorplac 
//linknuti vice ceduli k jednomu placu...
if (!(safe.link.isvendorplac))//&&(sector.allitems(findvendorplac(<uid>))))//neni linknut&&nalezl v sektoru vendorplac
 //src.classmessage(nemame link)//, za to mame vendorplac)
 tag(vm_lockit,0)//nastaveni zakladni dostupnosti menu 
 var(vendorplacfound,0)
 sector.allitems(findvendorplac(<uid>)) //uid te cedule 
 //src.classmessage(nalezen vendorplac: <var(vendorplacfound)>)
 if (var(vendorplacfound))
  //src.classmessage(vendorplac nalezen)
  link=var(vendorplacfound)
  src.classmessage(V tomto sektoru nalezen prostor pro vendora <?finduid(link).name?>. Menu prirazeno.)
  //return 1
  else
  src.classmessage(Nebyl nalezen vendorplac, na ktery by cedule sla linknout.)
 endif
endif

//nasledujici blok se provede jen tehdy, neexistuje-li v dane oblasti vendorplac vubec
if (!(safe finduid(link.tag(ownerscont)).isitem))//neni aktivovan (nema owneruv pytlicek)
 src.classmessage(Tento prostor pro vendora nyni patri tobe.)
 if !(safe link.isitem)//neni to multidum  -tomuhle moc nerozumim, necham to tak, treba to pujde, multidomy to nejsou
  src.classmessage(zakladam worldgem)
  src.newitemsafe(i_worldgem_bit)
  link=<lastnew>
  lastnew.attr=0b0
  lastnew.p=<p>
  lastnew.type=t_vendorplac
  lastnew.more1=<src>
  //link.tag(mammajitele,1)
  if (myrepregion)
   lastnew.name=<var(lastrepregion).name>
  else
   lastnew.name=<region.name>
  endif
 else
  link.events +t_vendorplac
  //src.classmessage(Zadne klice od nej nebudes potrebovat)
 endif
 
 src.newitemsafe(i_pouch)//ownerovo
 lastnew.attr_invis=1
 lastnew.attr_movenever=1
 lastnew.P=<P>
 lastnew.nudgeup(18)
 lastnew.more1=1 //majitelovo cislo
 lastnew.link=<link>
 lastnew.name=majitel
 link.tag(ownerscont,<lastnew>) //link uz je nyni ten worldgem
 
 src.newitemsafe(i_vendor_chest)
 lastnew.attr=attr_move_never
 lastnew.P=<P>
 lastnew.fix
 lastnew.link=<link>
 lastnew.name=Najem
 link.tag(najemcont,<lastnew>) //link=ten worldgem...
 link.tag(paiduntil,<serv.time>) //vynuluje se najem
 link.tag(najem,100000)//100k default
 if (region.tag(housenumber))
   link.tag(housenumber,<region.tag(housenumber)>)
   var(najmy[link.tag(housenumber)],<link>)
 else
   link.tag(housenumber,<array_add(var.najmy,<link>)>)
 endif
 link.tag(block,0)
 link.tag(autorent,1) //to je asi rozumny, zejo...
 link.tag(drazba,0)
 link.tag(vendpos_nastavena,0)
 link.tag(vendorplaced,0)
 link.f_vend_ownershipto(<src>) //src - ten co klika na ceduli
 src.sysmessage(Najem cini <link.tag(najem)>gp)
 src.redmessage(Autorent zapnut, zaplat si najem jeste nez podruhe kliknes na ceduli!) 
 //return 1
endif

//kontrola pritomnosti najemni truhlicky (jednou se mi stalo ze zmizela...)
if !(finduid(link.tag(najemcont))).isitem //neexistuje-li truhlicka
 src.newitemsafe(i_vendor_chest)
 lastnew.attr=attr_move_never
 lastnew.P=<P>
 lastnew.fix
 lastnew.link=<link>
 lastnew.name=Najem
 link.tag(najemcont,<lastnew>) //link=ten worldgem...
 if (region.tag(housenumber))
   link.tag(housenumber,<region.tag(housenumber)>)
   var(najmy_stanky[link.tag(housenumber)],<link>)
 else
   link.tag(housenumber,<array_add(var.najmy_stanky,<link>)>)
 endif
 src.sysmessage(Truhlicka navracena!)
 src.sysmessage(Najem cini <link.tag(najem)>gp)
endif

link.f_checkvendornajem
if !(link.more1)//dum je bez majitele :o) - tohle uz se neprovede nikdy
 src.classmessage(Dum je bez majitele)
 if !(src.isgm)
  link.potvrdit(myvendorplac,Chces obsadit toto prodejni misto?) //pokud se zvoli ANO (OK button) provede se myvendorplac
  return 1
 endif
endif

//nasledujici konstrukce overi pritomnost staryho hracskyho vendora (pouze pokud klika na ceduli GM!)
if(<src.isgm>) 
 //src.sysmessage(Budu hledat vendora)
 if (!(link.tag(vendorplaced))) //neni li na barak linkovan zadny vendor
  //src.sysmessage(Hledam vendor)
  var(vendorfound,0) 
  sector.allchars(findvendor(<uid>))) //melo by nalezt vendora v sektoru v nemz lezi link s timto UID (tj. cedule
  if(var(vendorfound)) //nalezl stareho vendora
   src.sysmessage(Stary hracsky vendor nalezen a zaznamenan)
   link.tag(vendorplaced,1)
   link.tag(vendpos_nastavena,1) //nastaveni pozici 
   link.tag(vendpos,<finduid(var(vendorfound)).p>) //ulozena pozice stareho vendora
   link.tag(vendoruid,<var(vendorfound)>) //ulozeno vendorovo UID
  endif
 endif
endif

//nasledujici konstrukce zamerne nepouziva elseifu protoze nejak nefungujou (neprovadeji se), proto je to tak 
//komplikovany
if(<link.tag(drazba)>) //probiha-li drazba
 if(<link.more1>==094dfd) //majitelem jsem ja - drazba jen 1 tyden
  arg(doba,6048000) 
  arg(max_doba,6048000)
 else //klasicke 3+1 tyden
  arg(doba,18144000)
  arg(max_doba,24192000)	
 endif
 arg(doba_od_zacatku_drazby,<eval <serv.time>-<link.tag(zacatekdrazby)>>)
 if(<arg(doba_od_zacatku_drazby)> > <arg(doba)>)&&(finduid(<link.more1>).isgm) //prvni drazba noveho stanku, majitelem jsem ja
  //if(src==094dfd)
   //src.sysmessage(konec drazby noveho stanku)
  //endif
  konec_drazby
  return 1	
 endif
 if((<arg(doba_od_zacatku_drazby)> >= <arg(doba)>)&&(<arg(doba_od_zacatku_drazby)> <= <arg(max_doba)>)&&(<link.tag(zajemce)>!=<link.more1>)&&(<src>==<link.more1>)) //dele nez (18144000)3 tydny&&kratsi dobu nez (24192000) 4 tydny&&nejvyssi nabidka neni od majitele&&klikatel na ceduli je majitel
  //if(src==094dfd)
   //src.sysmessage(Majitel ma cas na dorovnani)
  //endif
  src.redmessage(Na dorovnani nabidky ti zbyva <f_vend_zbyva_casu>)
  link.dialog d_vendor_menu
  return 1
 endif
 if((<arg(doba_od_zacatku_drazby)>> <arg(max_doba)>)&&(<link.tag(zajemce)>!=<link.more1>)&&(<src>==<link.tag(zajemce)>)) //doba drazby delsi nez (24192000) 4 tydny&&nejvyssi nabidka neni od majitele&&kliknul si ten co nabidl nejvic
  //if(src==094dfd)
   //src.sysmessage(konec drazby a klikl ten co vyhral)
  //endif
  konec_drazby
  return 1
 endif
 if((<arg(doba_od_zacatku_drazby)>> <arg(max_doba)>)&&(<link.tag(zajemce)>!=<link.more1>)&&(<src>==<link.more1>)) //konec drazby&&majitel nestihl dorovnat&&klikl majitel
  //if(src==094dfd)
   //src.sysmessage("Konec drazby majitel prohral a klikl majitel")
  //endif
  src.sysmessage("<src.sex(Nestihl,Nestihla)> jsi dorovnat nabidku. Stanek ti uz nepatri")
  konec_drazby
  return 1
 endif
 if((<arg(doba_od_zacatku_drazby)>> <arg(max_doba)>)&&(<link.tag(zajemce)>==<link.more1>)&&(<src>==<link.tag(zajemce)>)) //doba drazby > (2419200) 4 tydny&&majitel stihl dorovnat&&klikatel je sam majitel
  //if(src==094dfd)
   //src.sysmessage(konec drazby vyhral puvodni majitel)
  //endif
  src.sysmessage(Drazba skoncila, majitelem zustavas ty)
  //src.redmessage(Zkontroluj si najem!)
  src.sysmessage(Najem prepocten a pridan jeden mesic)    
  link.tag(drazba,0) //konec drazby
  arg(zaplaceno_dni,<link.paiduntildays>)
  arg(stary_najem,<link.tag(najem)>)
  link.tag(najem,<link.tag(nabidka)>) //novy najem = nejvyssi nabidka
  arg(novy_najem,<link.tag(najem)>)
  arg(pocet_dni_nova_cena_najmu,<eval (<arg(zaplaceno_dni)>*<arg(stary_najem)>)/<arg(novy_najem)>>) //pocet zaplacenych dni z puvodniho najmu pri nove cene
  link.tag(paiduntil,<eval (<serv.time>+(<arg(pocet_dni_nova_cena_najmu)>*864000))+25920000>) //v decisekundach: prepocte stare zaplaceni na novy najem a prida 1 mesic za drazbu
  src.sysmessage(Novy najem cini <link.tag(najem)>gp.)
  link.tag.remove(nabidka)
  link.tag.remove(zacatekdrazby)
  link.tag.remove(zajemce)
  link.DIALOG d_vendor_menu
  return 1
 endif
 if((<arg(doba_od_zacatku_drazby)> >= <arg(doba)>)&&(<arg(doba_od_zacatku_drazby)> <= <arg(max_doba)>)&&(<src>!=<link.more1>)&&(!(src.isgm))) //doba drazby >(1814400)3 tydny&&kratsi nez (2419200)4 tydny&&kliknul nekdo jinej nez majitel&&a nekliknul GM   
   //if(src==094dfd)
    //src.sysmessage(cas na dorovnani pro majitele)
   //endif
   src.sysmessage(Prihazovat jiz nemuzes, majitel ma na dorovnani nabidky <f_vend_zbyva_casu>)
   return 1
 endif  
 if((<arg(doba_od_zacatku_drazby)> > <arg(max_doba)>)&&(<src>!=<link.more1>)&&(<src>!=<link.tag(zajemce)>)&&(!(src.isgm))) //doba drazby >(24192000) 4tydny&&neklikl majitel&&neklikl nejvyssi nabidka&&neklikl GM
  //if(src==094dfd)
   //src.sysmessage(konec drazby neklikl ani ten co vyhral ani majitel a majitel prohral)
  //endif
  src.sysmessage(Konec drazby, nejsi majitelem stanku ani jsi neucinil nejvyssi nabidku, nezbyva ti, nez to zkusit priste)   
  konec_drazby
  return 1
 endif
 if((<arg(doba_od_zacatku_drazby)> > <arg(doba)>)&&(<arg(doba_od_zacatku_drazby)> < <arg(max_doba)>)&&(<link.tag(zajemce)>==<link.more1>)&&(<src>==<link.more1>)) //dele nez (18144000)3 tydny&&kratsi dobu nez (24192000) 4 tydny&&nejvyssi nabidka je uz od majitele&&klikatel na ceduli je majitel
  //if(src==094dfd)
   //src.sysmessage(konec drazby majitel ma cas na dorovnani a dorovnal - drazba konci drive)
  //endif
  src.sysmessage(Nabidka je dorovnana, do konce ctvrteho tydne zbyva <f_vend_zbyva_casu>)
  link.dialog d_vendor_menu
  return 1
 endif 
endif

link.DIALOG d_vendor_menu
return 1

[function konec_drazby] //src- klikatel, uid - cedule
arg(majitel,<link.more1>)
link.trashvendorplac
if(link.tag(vendorplaced)) //existuje-li vendor
 if (strcmpi(<link.tag(vendoruid).tag(mainowner)>,<link.tag(zajemce)>)) //neni-li vendoruv majitel ten co vyhral drazbu (tyka se prvni drazby)
  link.trashvendor_itagy //vyhodi vendora. pokud vyhral drazbu majitel vendora tak se nic nevyhazuje
 endif
endif
arg(zaplaceno_dni,<link.paiduntildays>) //kolik mel starej majitel zpalaceno
arg(star_najem_den,<eval <link.tag(najem)>/30>) //kolk cinil starej najem/den 
arg(vratit_majiteli,<eval (<arg(star_najem_den)>*<arg(zaplaceno_dni)>)>) //puv. majitel dostane tolik do banky
//src.sysmessage(zaplaceno: <arg(zaplaceno_dni)>, starnajem: <arg(star_najem_den)>, vratit: <arg(vratit_majiteli)>)
src.newitemsafe(i_cashiers_check)
//src.sysmessage(sek vytvoren <lastnew>)
lastnew.more=<arg(vratit_majiteli)>
lastnew.name=Preplatek najmu z prodejniho stanku 
lastnew.cont=<finduid(<arg(majitel)>).findlayer(29)> //banka stareho majitele, ale stejne mu to dava do batohu
lastnew.update
link.more1=<link.tag(zajemce)> //novy majitel prodejniho mista - posledni nejvyssi nabidka
link.tag(drazba,0) //konec drazby  
link.tag(najem,<link.tag(nabidka)>) //posledni nabidka = novy najem
link.tag(paiduntil,<eval <serv.time>+25920000>) //najem na mesic
finduid(<link.more1>).delayedmessage(Jsi novym majitelem stanku, cislo <link.tag(housenumber)>)
finduid(<link.more1>).delayedmessage(Najem cini <link.tag(najem)>gp)
finduid(<link.more1>).delayedmessage(Najem zaplacen na jeden mesic dopredu)
link.tag.remove(nabidka)
link.tag.remove(zacatekdrazby)
link.tag.remove(zajemce)
if(<src>==<link.more1>) //kliknul novy majitel
 link.DIALOG d_vendor_menu
endif
return 1

[function myvendorplac]
if (ischar) //jestlize to provadi hrac tak nedelat nic 
elseif (isvendorplac) //jestlize to provadi item vendorplac (to je to volani link.potvrdit(...) - o kus vys
 f_vend_ownershipto(<src>) //udela to majitelem toho co klikal na ceduli
endif
// tag(paiduntil,<serv.time>) //a nastavi zaplacenost najmu
DIALOG d_vendor_menu

[function f_vend_zbyva_casu]
arg(doba_od_konce_3tydnu,<eval ((<serv.time>-<link.tag(zacatekdrazby)>)-18144000)>) //celk.doba - 3 tydny   
arg(zbyva_casu,<eval ((24192000-18144000)-arg(doba_od_konce_3tydnu))>) //1 tyden - uplynula doba z nej
arg(dnu,<eval (arg(zbyva_casu)/864000)>) //den ma 86400s (tohle je v decisekundach)
arg(hodin,<eval ((arg(zbyva_casu)-(arg(dnu)*864000))/36000)>) //hodina ma 3600s
arg(minut,<eval (((arg(zbyva_casu)-(arg(dnu)*864000))-(arg(hodin)*36000))/600)>)
arg(sekund,<eval ((((arg(zbyva_casu))-(arg(dnu)*864000))-(arg(hodin)*36000))-(arg(minut)*600))/10>)
return(<arg(dnu)/10> dni <arg(hodin)/10> hodin <arg(minut)/10> minut <arg(sekund)/10> sekund)

[function vm_namerank]
doswitch <args>
 var(namestr,Verejnost)
 var(namestr,Majitel)
 var(namestr,GM)
enddo
return"<namestr>"

[function vm_findrank]
var(vm_rank,0) //verejnost
if (finduid(<args>).isgm)
 var(vm_rank,2) //GM
elseif (<more1>==<args>)
 finduid(<tag.vend_ownermemUID>).more1=<uid>
 finduid(<tag.vend_ownermemUID>).morep=<p>
 finduid(<tag.vend_ownermemUID>).name=owner:<name>
 var(vm_rank,1) //majitel
//else
// finduid(<tag.friendscont>).contents(vm_findrankloop(<args>))
// if (var(vm_rank)==0)
//  finduid(<tag.coownerscont>).contents(vm_findrankloop(<args>))
// endif
endif
if (safe tag(block))//&&(vm_rank<4)
 if (var(vm_rank))
  src.redmessage(Prostor pro vendora je zablokovan! Pro vice informaci napis page.)
 endif
 if (var(vm_rank)<2)
  var(vm_rank,-1)
 endif
endif
return <var(vm_rank)>

[function vm_findrankloop]
if !(link.isitem)
remove
return 0
endif
link.name=<cont.name>:<cont.link.name>
name=<link.topobj.name>
link.morep=<p>
link.more1=<cont.link>
if (<link.topobj>==<args>)
 var(vm_rank,<cont.more1>)
endif

[function vm_section]//y,index,rank,text
if (var(vm_rank)>=argv(2)) //argv(2)=rank
 texta(<tag(sloupec_x[1])>+<d_def_odsazeni>,argv(0),955,<argv(3)>)//ban
 button(<tag(sloupec_x[0])>,argv(0),4005,4007,1,0,argv(1))
 return <eval argv(0)+d_def_radek_vyska>
endif
return <argv(0)>

[DIALOG d_vendor_menu]
30,0
arg(itemlevel,<eval tag(lastusedsign).tag(vm_lockit)>)
//src.sysmessage(<arg(itemlevel)>)
vm_findrank(<src>)
//var(vm_rank,0)
vm_namerank(<var(vm_rank)>) //0,1,2 - verejnost, majitel, GM
//src.sysmessage(Tvuj rank je: <var(vm_rank)>; Tvuj namerank je : <namestr>)  - V PORADKU
//src.smsg hm_rank:<hm_rank> itemlevel:<arg(itemlevel)>
argo.settext(100,"Stanek cislo <?eval tag(housenumber)?><br><?safe tag(housename)?>")

if (var(vm_rank)<arg(itemlevel))//rank je 0-2, item level o par radku vyse podle lockitu.lockit je zakladne na 0
 argo.gumppic(100,100,100)
 argo.htmlgump(120,120,100,60,100,0,0)
else
 doswitch var(vm_rank)  //0,1,2 
  argo.tag(radku,3)  //cudliku verejnosti
  argo.tag(radku,10)  //cudliku majitele
  argo.tag(radku,19) //cudliku GM
 enddo
 argo.tag(sirka,260)
 //argo.tag(vyska,360)
 
 //if (cm_displaymore)
 argo.tag(vyska,<eval 100+((2+argo.tag(radku))*<d_def_radek_vyska>)+(2*<d_def_okraj>)+(4*<d_def_skvira>)>)
 //else
 // argo.tag(vyska,<eval (6*<d_def_radek_vyska>)+(2*<d_def_okraj>)+(4*<d_def_skvira>)>)
 //endif
 
 argo.dialog_prvni
 argo.dialog_pozadi(<argo.tag(nexty)>,1,140+d_def_skvira)//owner
 argo.dialog_pozadi(<argo.tag(nexty)>,1,140+d_def_skvira)//owner
 argo.dialog_pozadi(<argo.tag(nexty)>,1,140+d_def_skvira)//rankname
 argo.dialog_pozadi(<argo.tag(nexty)>,1,140+d_def_skvira)//rankname
 arg(vedleX,<argo.tag(sloupec_x[1])>+<d_def_odsazeni>)
 argo.dialog_pozadi(114+d_def_skvira,2)//najem
 argo.dialog_pozadi(<argo.tag(nexty)>,<argo.tag(radku)>,29)//tlacitka
 argo.dialog_zpruhledni
 //argo.resizepic(0,0,2620,290,360)
 argo.gumppic(<argo.dialog_textpos(0,0,1)>,100)
 //argo.htmlgump(120,120,100,60,0,0,0)
 argo.htmlgump(lastxpos+20,lastypos+20,100,60,100,0,0)//
 argo.texta(vedleX,argo.tag(obj_y[0]),955,Majitel:)
 argo.texta(vedleX,argo.tag(obj_y[1]),955,<finduid(more1).getrealnamebyrealm(<region.tag.houserealm>)>)
 argo.texta(vedleX,argo.tag(obj_y[2]),955,Tvuj status:)
 argo.texta(vedleX,argo.tag(obj_y[3]),955,<namestr>)
 
 
 argo.texta(<argo.dialog_textpos(4,0)>,955,Najem: <tag.najem> gp)
 argo.texta(lastxpos,lastypos+d_def_radek_vyska,955,Zaplaceno na <paiduntildays> dni)//<sectodays((tag(paiduntil)-serv.time)/10)> 
 
 arg(ypos,<argo.tag(obj_y[5])>)
 arg(ypos,<argo.vm_section(<ypos>,1,0,Drazebni nabidka)>)
 arg(ypos,<argo.vm_section(<ypos>,2,1,Zamknout/odemknout vec)>)
 //arg(ypos,<argo.vm_section(<ypos>,3,0,<qval(f_vend_dorprih_mjtl_gm,Dorovnat <tag(nabidka)>,Prihodit <qval(<tag(nabidka)>,<eval <tag(nabidka)>+50000>,<eval <tag(najem)>+50000>)>)>)>) //majitel+gm pouze dorovnat, verejnost Prihodit 50k
 arg(ypos,<argo.vm_section(<ypos>,19,0,<qval(f_vend_dorprih_mjtl_gm,Dorovnat <tag(nabidka)>,Prihodit)>)>)//<qval(<tag(nabidka)>,<eval <tag(nabidka)>+50000>,<eval <tag(najem)>+50000>)>)>)>)
 arg(ypos,<argo.vm_section(<ypos>,4,2,Prihodit)>) //GM ma jeste navic tuto moznost
 arg(ypos,<argo.vm_section(<ypos>,5,1,<qval(<tag(vendorplaced)>,Pre,U)>misteni Vendora)>)
 arg(ypos,<argo.vm_section(<ypos>,6,1,Zmena pohlavi Vendora)>)
 arg(ypos,<argo.vm_section(<ypos>,7,1,Prejmenovat stanek)>)
 
 arg(ypos,<argo.vm_section(<ypos>,9,0,"<qval(<tag(bankactivated)>,"Otevrit banku","Aktivovat banku (100k gp)")>")>)
 arg(ypos,<argo.vm_section(<ypos>,10,1,Predat vlastnictvi stanku)>)
 
 arg(ypos,<argo.vm_section(<ypos>,11,2,<qval(<tag(block)>,Od,Za)>blokovat stanek)>)
 arg(ypos,<argo.vm_section(<ypos>,12,2,Trash stanek (necha vendora))>) //=trashvendorplac
 arg(ypos,<argo.vm_section(<ypos>,8,2,<qval(<tag(vendor_trest)>,Vratit vendora z trestu,Odstranit vendora (trest))>)>) //trashvendor
 arg(ypos,<argo.vm_section(<ypos>,18,2,Odstranit vendora (uplne))>) //trashvendor_itagy
 arg(ypos,<argo.vm_section(<ypos>,13,2,Vynulovat zaplaceni)>)
 arg(ypos,<argo.vm_section(<ypos>,14,2,Nastavit najem)>)
 arg(ypos,<argo.vm_section(<ypos>,15,2,<qval(<tag(autorent)>,Vy,Za)>pnout autorent)>) 
 arg(ypos,<argo.vm_section(<ypos>,16,2,Zastavit drazbu)>) 
 arg(ypos,<argo.vm_section(<ypos>,17,1,Nastavit pozici pro vendora)>)
 arg(ypos,<argo.vm_section(<ypos>,20,1,Help)>)
endif

[DIALOG d_vendor_menu BUTTON]
on=1//Drazebni nabidka
if (f_vend_checkrightsmes(0))
DIALOG d_vendmenu_nabidka
endif

on=2//zamknout/odemknout vec
if (f_vend_checkrightsmes(1))
//src.say chci zamknout vec
src.newitemsafe(i_target_vendor)
lastnew.name=zamknout vec
lastnew.link=<uid>
lastnew.more1=2
lastnew.more2=<vm_rank>
lastnew.equip
endif

//on=3//dorovnat / prihodit 50k
//if (f_vend_checkrightsmes(0))
// if(!(<var(vm_rank)>)) //zahajit drazbu nebo prihodit 50k (pro verejnost)    
//   if(!<tag(drazba)>) //neprobiha-li zrovna drazba pozn. vsechny tagy jdou na ten worldgem
//      if(src.pay((<tag(najem)>+50000))) 
//       tag(nabidka,<tag(najem)>+50000) //link je porad ten worldgem t_vendorplac
//       tag(zajemce,<src>) //uid zajemce
//       tag(zacatekdrazby,<serv.time>)
//       tag(drazba,1) //zahajit drazbu
//       src.sysmessage(Ucinil jsi zakladni nabidku <tag(nabidka)>gp.)
//      else
//       src.sysmessage(Nemas dost penez na zahajeni drazby, potrebujes aspon <eval <tag(najem)>+50000> gp.)
//      endif    
//   else //probiha-li drazba
//      arg(uplynula_doba_drazby,<serv.time>-<eval tag(zacatekdrazby)>)
//      if(<arg(uplynula_doba_drazby)> < 18144000) //cas od zacatku drazby je mensi nez tri tydny (3*7*24*3600*10=18144000)
//         if(src.pay((<tag(nabidka)>+50000)))
//           tag(nabidka,<tag(nabidka)>+50000) //autorem je porad ten worldgem t_vendorplac
//           tag(zajemce,<src>)
//          src.sysmessage(Zvysil jsi nabidku o 50k gp. Nejvyssi nabidka je nyni <tag(nabidka)>gp)
//          else
//           src.sysmessage(Nemas dost penez na prihozeni, potrebujes <eval <tag(nabidka)>+50000> gp.)
//          endif
//       else
//          src.sysmessage(Drazba uz skoncila)
//      endif
//   endif
// else //((var(vm_rank))||(var(vm_rank)==2)) //dorovnavat    
//    if(<tag(drazba)>) //probiha-li drazba
//      if(!(<tag(zajemce)>==<more1>)) //0 pokud stejne
//        if(src.isgm)
//          src.sysmessage(Jako GM muzes dorovnat zdarma)
//          src.accmsg(dorovnal nabidku v drazbe stanku <name> ve prospech majitele <more1.name>)
//          tag(zajemce,#<more1>) //GM dorovnal ve prospech majitele (zajemce tudiz bude = majiteli)
//        else //neni to gm, je to majitel
//         if(src.pay(<tag(nabidka)>)) //jestlize si muze majitel dovolit dorovnat
//            tag(zajemce,<src>) // majitel dorovnal, stal se nejvyssi nabidkou ale samotna max. nabidka se nezvysuje...
//            src.sysmessage(Dorovnal jsi nejvyssi nabidku)
//          else
//            src.sysmessage(Nemas dost penez k dorovnani nabidky)
//          endif
//        endif
//      else //retezce byly stejne
//        src.sysmessage(Nejvyssi nabidka patri majiteli, neni treba dorovnavat)
//      endif
//    else //drazba neprobiha
//     src.sysmessage(Neprobiha zadna drazba, neni potreba nic dorovnavat) 
//    endif   
// endif
// dialog d_vendmenu_nabidka
//endif

on=4//prihodit (GM)
if (f_vend_checkrightsmes(2))
  if(!<tag(drazba)>)  
    input(Jaka ma byt nabidka? nejmene <eval <tag(najem)>+50000> ,f_vend_prihod_gm,<eval 50000+<tag(najem)>>)
  else //probiha-li drazba
    arg(uplynula_doba_drazby,<serv.time>-<eval tag(zacatekdrazby)>)
    if(<arg(uplynula_doba_drazby)> < 18144000) //cas od zacatku drazby je mensi nez tri tydny (3*7*24*3600*10=18144000)
      input(Nova nabidka? nejmene <eval <tag(nabidka)>+50000> ,f_vend_prihod_gm,<eval 50000+<tag(nabidka)>>) 
    else
      src.sysmessage(Drazba uz skoncila)
    endif       
  endif
endif

on=5// U/Pre mistit vendora vendora
if (f_vend_checkrightsmes(1))
  if(<tag(vendorplaced)>) //je li vendor umisten => cudlik Premisti
   if(<tag(vendor_trest)>) //je li vendor za trest odstranen
    if(src.isgm)
      src.sysmessage(Vendor odstranen za trest, chces li jej vratit pouzij prislusny cudlik!)
    else
      src.redmessage(Vendor odstranen za trest, napis page pro vyreseni situace)
    endif
   else
    arg(vendorfound,<tag(vendoruid)>)
    //src.sysmessage(sector...)
    //sector.allchars(findvendor(<uid>)) //melo by nalezt vendora v sektoru v nemz lezi item s timto UID (tj. cedule)
    //src.sysmessage(po sectoru)
    if(arg(vendorfound)) //mame-li vendora (vratil vendorovo UID)
      //src.sysmessage(<tag(vendpos)>, <finduid(arg(vendorfound)).p>)  
      if(!(strcmpi((<tag(vendpos)>),(<finduid(arg(vendorfound)).p>)))) //je li pozice stale ta stara (rovnost => vraci 0)
        src.sysmessage(Neni potreba ho premistovat, na urcenem miste uz stoji)
        src.sysmessage(Pro premisteni vendora nastav napred jeho novou pozici)
      else 
        finduid(arg(vendorfound)).p=<tag(vendpos)> //premisti na novou pozici
        finduid(arg(vendorfound)).update
        src.sysmessage(Vendor premisten)
      endif
    else 
      src.sysmessage(Vendor nenalezen!)      
    endif
   endif
  else //vendor jeste nestoji =>cudlik Umisti
    if(!<tag(vendpos_nastavena)>) //nemame pozici
      src.sysmessage(Neni nastavena pozice!)
    else //pozice je
      src.newnpc=c_h_vendor //nasedujici nastaveni vendora je z casti obslehnuto z vendor deedu
      lastnewchar.name=Vendor 
      lastnewchar.tag(mainowner,<src>)
      lastnewchar.food=<lastnewchar.def.maxfood>
      lastnewchar.p=<tag(vendpos)>
      lastnewchar.update
      lastnewchar.newmemory(<src>)
      lastnewchar.act.color=memory_ipet
      tag(vendorplaced,1) //nastavit tag indikujici umisteni 
      tag(vendoruid,<lastnewchar.uid>)
      src.sysmessage(Vendor uspesne pridan)
    endif
  endif
endif

on=6//zmena pohlavi vendora
if (f_vend_checkrightsmes(1))
  if(<tag(vendorplaced)>)
    if(<tag(vendor_trest)>)
     src.redmessage(Vendor je za trest odstranen, napis page pro vyreseni situace)
    else
     if(finduid(<tag(vendoruid)>).body==c_man)
        finduid(<tag(vendoruid)>).body=c_woman
        finduid(<tag(vendoruid)>).speech=spk_vendor_custom  //protoze to maze jejich rec   
     else
        finduid(<tag(vendoruid)>).body=c_man   
        finduid(<tag(vendoruid)>).speech=spk_vendor_custom  //protoze to maze jejich rec   
     endif
     src.sysmessage(Pohlavi vendora zmeneno.)    
    endif
  else
    src.sysmessage(Neni tu vendor, nemuzes menit nic)
  endif
endif

on=7//prejmenovat prodejni misto
if (f_vend_checkrightsmes(1))
//src.say chci prejmenovat dum
 //src.smsg Jak se ma dum jmenovat?
 //src.targ=<uid>
 //src.everbtarg finduid(<uid>).renamehouse=tag(housename)
 input(Jak se ma stanek jmenovat?,tag.housename,<tag(housename)>)
endif

on=8//vyhodit vendora
if (f_vend_checkrightsmes(2))
 if(<tag(vendor_trest)>) //Navratit vendora
  arg(vendorfound,<tag(vendoruid)>)
  if(arg(vendorfound)) //mame-li vendora (vratil vendorovo UID)
    finduid(arg(vendorfound)).p=<tag(vendpos)> //premisti na novou pozici
    finduid(arg(vendorfound)).update
    src.sysmessage(Vendor navracen)
  else 
    src.sysmessage(Vendor nenalezen - pravdepodobne byl zrusen po potrestani!)      
    tag.remove(vendor_trest)
  endif
  tag.remove(vendor_trest)
 else //Odstranit vendora za trest
  potvrdit(trashvendor,"Opravdu chces odstranit vendora? Nezrusi tagy => majitel si nemuze pridat noveho vendora - pouzij pouze jako TREST nikoliv k vyklizeni stanku !!!")
 endif 
endif

on=9//banka
if (tag(bankactivated))
  if ((distancefrom(<src>) < 4) || (<region> == <src.region>))
   if (<src.tag.realm>==<region.tag.houserealm>)
    src.bankself
   else
    src.sysmessage("K teto funkci nemas prava.")
   endif 
  else
    src.sysMessage("Stojis daleko")
  endif
else
  potvrdit("aktivuj_banku", Skutecne chces aktivovat banku v tomto stanku a zaplatit za to 100 000 gp?)
endif

on=10//predat vlastnictvi
if (f_vend_checkrightsmes(1))
//src.say chci predat vlastnictvi domu.
src.newitem i_target_vendor
src.act.name=vlastnik
src.act.link=<uid> 
src.act.more1=10
src.act.equip
endif

on=11//block plac
if (f_vend_checkrightsmes(2))
 potvrdit(f_vend_blockplac,"Opravdu chces stanek <qval(<tag(block)>,Od,Za)>blokovat?")
endif

on=12//trash plac
if (f_vend_checkrightsmes(2))
 potvrdit(trashvendorplac,"Opravdu chces vyklidit stanek?") 
endif

on=13//vynulovat najem
if (f_vend_checkrightsmes(2))
 potvrdit(tag(paiduntil,<serv.time>),"Opravdu chces vynulovat najem?")
endif

on=14//nastavit najem
if (f_vend_checkrightsmes(2))
 //src.smsg Kolik gp ma byt mesicni najem?
 //src.everbtarg finduid(<uid>).tag.najem=
 input("Kolik ma byt najem?",tag.najem,<tag(najem)>)
endif

on=15//autorent
if (f_vend_checkrightsmes(2))
 if (tag(autorent))
  src.smsg Automaticky najem vypnut.
  tag(autorent,0)
 else
  src.smsg Automaticky najem zapnut.
  tag(autorent,1)
 endif
endif

on=16//zastavit drazbu
if (f_vend_checkrightsmes(2))
 potvrdit(f_vend_zastavit_drazbu,"Opravdu chces zastavit drazbu tohoto stanku?")
 return 1
endif

on=17//pozice pro Vendora
if (f_vend_checkrightsmes(1))
 src.newitemsafe(i_target_vendor)
 lastnew.link=<uid>
 lastnew.more1=17
 lastnew.equip
endif

on=18//odstranit vendora i s tagy
if (f_vend_checkrightsmes(2))
 potvrdit(trashvendor_itagy,"Opravdu chces odstranit vendora? Odstraneni i s tagy => pokud nechas majitele muze si pridat noveho vendora!")
 return 1
endif

on=19//prihodit libovolne
if (f_vend_checkrightsmes(0))
  if(!(<var(vm_rank)>)) //zahajit drazbu nebo prihodit (pro verejnost)    
    if(!<tag(drazba)>) //neprobiha-li zrovna drazba pozn. vsechny tagy jdou na ten worldgem
      input(Jaka ma byt prvni nabidka? nejmene <eval <tag(najem)>+50000>,f_vend_prihod,<eval 50000+<tag(najem)>>)     
    else //probiha-li drazba
      arg(uplynula_doba_drazby,<serv.time>-<eval tag(zacatekdrazby)>)
      if(<arg(uplynula_doba_drazby)> < 18144000) //cas od zacatku drazby je mensi nez tri tydny (3*7*24*3600*10=18144000)
         input(Nova nabidka? nejmene <eval <tag(nabidka)>+50000> ,f_vend_prihod,<eval 50000+<tag(nabidka)>>)
      else
        src.sysmessage(Drazba uz skoncila)
      endif    
    endif 
  else //((var(vm_rank))||(var(vm_rank)==2)) //dorovnavat    
    if(<tag(drazba)>) //probiha-li drazba
      if(!(<tag(zajemce)>==<more1>)) //posledni zajemce v drazbe neni majitel
        if(src.isgm) //klikl GM
          src.sysmessage(Jako GM muzes dorovnat zdarma)
          src.accmsg(dorovnal nabidku v drazbe o "<name>" ve prospech majitele "<more1.name>")
          tag(zajemce,#<more1>) //GM dorovnal ve prospech majitele (zajemce tudiz bude = majiteli)
        else //neni to gm,  klikl majitel
          if(src.pay(<tag(nabidka)>)) //jestlize si muze majitel dovolit dorovnat
            tag(zajemce,<src>) // majitel dorovnal, stal se nejvyssi nabidkou ale samotna max. nabidka se nezvysuje...
            src.sysmessage(Dorovnal jsi nejvyssi nabidku)
	    src.accmsg(Dorovnal jako majitel nabidku ve stanku <name>)
          else
            src.sysmessage(Nemas dost penez k dorovnani nabidky, potrebujes <tag(nabidka)> gp.)
          endif
        endif
      else //posledni zajemce v drazbe uz je majitel sam
        src.sysmessage(Nejvyssi nabidka patri majiteli, neni treba dorovnavat)
      endif
    else //drazba neprobiha
      src.sysmessage(Neprobiha zadna drazba, neni potreba nic dorovnavat) 
    endif   
  endif 
endif

on=20 // Help
src.dialog(d_vendor_help)
 
[function f_vend_prihod] //argv(0) - to co zadal v inputu za castku
arg(castka,<fixNumber(<args>)>)
if(!<tag(drazba)>) //zavolano z prvni casti buttonu 19
   if(<arg(castka)> < (<eval <tag(najem)>+50000>))
     src.sysmessage(Drazbu muzes zahajit vstupni castkou nejmene <eval <tag(najem)>+50000>)
   else 
     if(src.pay(<arg(castka)>)) 
       tag(nabidka,<arg(castka)>) //link je porad ten worldgem t_vendorplac
       tag(zajemce,<src>) //uid zajemce
       tag(zacatekdrazby,<serv.time>)
       tag(drazba,1) //zahajit drazbu
       if(!<strcmp(<more1>,"094dfd")>==0) // Jen pokud neni majitelem stanku Dini
         finduid(<more1>).delayedmessage("Hrac <src.name> zahajil drazbu na tvem stanku castkou <tag(nabidka)>")
       endif
       src.sysmessage(Ucinil jsi zakladni nabidku <tag(nabidka)> gp.)
       src.accmsg(Zahajil drazbu o stanek <name> nabidkou <castka>)
     else
       src.sysmessage(Nemas dost penez na zahajeni drazby, potrebujes aspon <eval (<tag(najem)>+50000)> gp.)
     endif   
   endif  
else //drazba neni, zavolano z druhe casti buttonu 19
   if(<arg(castka)> < (<eval <tag(nabidka)>+50000>))
     src.sysmessage(Nabidku muzes zvysit nejmene na <eval <tag(nabidka)>+50000>)
   else 
     if(src.pay(<arg(castka)>))
       tag(nabidka,<arg(castka)>) //autorem je porad ten worldgem t_vendorplac
       tag(zajemce,<src>)
       if(!<strcmp(<more1>,"094dfd")>==0) // Jen pokud neni majitelem stanku Dini
         finduid(<more1>).delayedmessage("Hrac <src.name> prihodil na tvuj stanek castkou <tag(nabidka)>")
       endif
       src.sysmessage(Zvysil jsi nabidku na <tag(nabidka)> gp.)
       src.accmsg(Zvysil nabidku v drazbe o stanek <name> castkou <castka>)
     else
       src.sysmessage(Nemas dost penez na prihozeni, potrebujes aspon <eval <tag(nabidka)>+50000> gp.)
     endif
   endif     
endif
dialog d_vendmenu_nabidka

[function f_vend_prihod_gm]
arg(castka,<fixNumber(<args>)>)
if(!<tag(drazba)>) //zavolano z prvni casti buttonu 4
   if(<arg(castka)> < (<eval <tag(najem)>+50000>))
     src.sysmessage(Drazbu muzes zahajit vstupni castkou nejmene <eval <tag(najem)>+50000>)
   else 
     src.sysmessage(Jako GM muzes drazit zdarma)
     src.accmsg(zahajil drazbu o "<name>" castkou <castka>)
     tag(nabidka,<arg(castka)>) //link je porad ten worldgem t_vendorplac
     tag(zajemce,<src>) //uid zajemce
     tag(zacatekdrazby,<serv.time>)
     tag(drazba,1) //zahajit drazbu
     src.sysmessage(Ucinil jsi zakladni nabidku <tag(nabidka)> gp.)     
   endif  
else //drazba neni, zavolano z druhe casti buttonu 4
   if(<arg(castka)> < (<eval <tag(nabidka)>+50000>))
     src.sysmessage(Nabidku muzes zvysit nejmene na <eval <tag(nabidka)>+50000>)
   else 
     src.sysmessage(Jako GM zvysujes nabidku zdarma)
     src.accmsg(prihodil do drazby o "<name>" castku <castka>)
     tag(nabidka,<arg(castka)>) //autorem je porad ten worldgem t_vendorplac
     src.sysmessage(Zvysil jsi nabidku na <tag(nabidka)> gp.)     
   endif     
endif
dialog d_vendmenu_nabidka

[function aktivuj_banku]
if (src.pay(100000)) 
  tag(bankactivated,1)
  src.accmsg("bank activated at vendorplac uid=<uid> (<name>)")
  src.classmessage(Banka aktivovana.)
  dialog d_vendor_menu
endif

[function f_vend_checkrightsmes] //overi prava uzivatele vyvesky vendora
if (f_vend_checkrights(<args>))
 return 1
else
 src.redmessage(Nemas na to pravo.)
 return 0
endif

[function f_vend_checkrights] //dtto co predchozi
vm_findrank(<src>)
if (<vm_rank>>=<args>)
 return 1
else
 return 0
endif

[function f_vend_zastavit_drazbu]
vm_findrank(<src>)
if(<vm_rank>==2) //GM
 tag(drazba,0)
 tag.remove(zajemce)
 tag.remove(nabidka)
 tag.remove(zacatekdrazby)
 src.accmsg(zastavil drazbu stanku <name>)
 src.sysmessage(Zastavil jsi drazbu stanku <name>)
 DIALOG d_vendor_menu
endif

[function f_vend_lockeditem]
if (finduid(tag.vm_lockedto)) //najde li uid cedule na kterou je to zamceno
 if (finduid(<tag.vm_lockedto>).isvendorplac)//je locknut k stanku
  var(globallock,<eval finduid(<tag.vm_lockedto>).tag.vm_lockit>)//uroven zamceni celeho stanku (defaultne 0)
  var(itemlevel,<tag.vm_lockit>)
  var(itemuid,0)
  if (<itemlevel>>2)
   var(itemuid,<itemlevel>) //ulozeni hodnoty
   finduid(<tag.vm_lockedto>).vm_findrank(<itemlevel>) //cedule nalezne rank toho itemu :)
   var(itemlevel,<vm_rank>+1)
  endif
  finduid(<tag.vm_lockedto>).vm_findrank(<src>)
  //src.smsg itemlevel:<itemlevel>. charlevel:<vm_rank>
  if ((type==t_door_locked)||(<itemlevel><=<vm_rank>)||(<itemuid>==<src>))&&(<globallock><=<vm_rank>)
  //dvere jsou zamcene - standart|| mensi itemlevel||jsem vlastnikem
   return 0
  else
   src.redmessage(Nemas na to pravo.)
   return 1
  endif
 endif
endif
events -t_vend_lockeditem
unblockitem
return 0

[DIALOG d_vend_statask]
0,0
if(<src.isgm>) //bude tam i cudlik GM
 argo.resizepic(100,100,2620,300,130)
else //pouze cudliky verjena a vlastnik
 argo.resizepic(100,100,2620,300,110)
endif

argo.settext(0,Na jakou uroven chces vec zamknout?) //uvodni text
argo.text(120,120,955,0) //uvodni text

argo.settext(1,Verejna)  //text 1
argo.text(140,145,955,1) //text cudliku 1 (verejnost)
argo.button(120,150,2104,2103,1,0,1)

argo.settext(2,Vlastnik)
argo.text(140,165,955,2)//text cudliku 4 (majitel)
argo.button(120,170,2104,2103,1,0,4)

if(<src.isgm>)
  argo.settext(3,GM)
  argo.text(140,185,955,3) //text cudliku 5 (GM)
  argo.button(120,190,2104,2103,1,0,5) 
endif

[DIALOG d_vend_statask BUTTON]
on=0
remove
on=1 //cudlik 1 (verejnost)
f_vend_lock(0)
//on=2
//f_lock(1)
//on=3 //cudlik 3 (GM)
//f_lock(2)
on=4 //vlastnik
f_vend_lock(1)
on=5 //GM
f_vend_lock(2)
//on=6
//if (src.uid==0)||(src.uid==1)||(src.uid==2)||(src.uid==3)||(src.uid==4)||(src.uid==5)
//  src.classmessage(Mas nevhodnou postavu na osobni zamykani...kontaktuj GM...)
//elseif (src.targ.type==<NID(t_housemenu)>)
//  src.sysmessage("Nemuzes zamknout vyvesku domu na osobu.")
//else
//  f_lock(<src>)
endif

[function f_vend_dorprih_mjtl_gm] //slouží pouze k rozhodnutí co zobrazit v menu -  jestli Dorovnat nebo Prihodit 50k
if(var(vm_rank)==0)               //v zavislosti na ranku postavy 
 return 0
endif
if(var(vm_rank)==1)
 return 1
endif
if(var(vm_rank)==2)
 return 1
endif
 
[function f_vend_lock]//link - house, src.targ - zamknuta vec, args - level
if (src.targ.issameregion(<link>))&&(src.targ.cont==0) //predmet ve stanku a na zemi
  if(src.targ==<link.tag(najemcont)>) //pokousel se zamykat truhlicku
    src.sysmessage(Truhlicku zamykat nemuzes)
    link.dialog d_vendor_menu
    return 1
  endif
  src.targ.tag(vm_lockit,<args>) //level zamceni
  link.array_findremove(tag.lockedlist,<src.targ>)//seznam itemu na domu
  link.array_add(tag.lockedlist,<src.targ>)//seznam itemu na domu
  //link.array_dump(tag.lockedlist)
  if (src.targ.type!=t_vendormenu)
   src.targ.tag(vm_lockedto,<link>) //uid cedule domu
   src.targ.tag(vm_storeattr,<src.targ.attr>) //ulozit atributy
   src.targ.attr_movenever=1 //a nastavit nove
   src.targ.attr_movealways=0
   src.targ.attr_decay=0
   src.targ.attr_candecay=0
   src.targ.events +t_vend_lockeditem
   src.classmessage(Vec zamknuta)
   if (<src.targ.type>==t_door)
    src.targ.type=t_door_locked
    src.targ.more1=<link>
    src.targ.link=<link>
   endif
  endif
  remove
endif

[function f_vend_blockplac]
if (argvcount)
 arg(value,<args>)
elseif (safe tag(block))
 arg(value,0) 
else
 arg(value,1) 
endif
if (arg(value))
 try tag(block,1)
 foreachlocked(f_vend_blockitem)
 src.redmessage(Stanek zablokovan.)
else
 try tag(block,"")
 foreachlocked(f_vend_unblockitem)
 src.classmessage(Stanek odblokovan.)
endif


[function f_vend_blockitem] //pro pripadne blokovani prodejniho mista 
if (<safe.tag.vm_lockedto>)
 if (finduid(<tag.vm_lockedto>).isvendorplac)//je locknut k domu
  if !(strlen(<tag.vm_storecolor>))//neni uz blocknut
   events +t_vend_lockeditem//pro jistotu
   tag(vm_storecolor,<eval color>)
   color=1
  endif 
 endif
endif

[function f_vend_unblockitem]
if (strlen(<tag.vm_storecolor>))
 color=<tag.vm_storecolor>
 tag(vm_storecolor,"")
endif

[DIALOG d_vendmenu_nabidka]
0,0
resizepic 100 100 2620 550 <eval (5*d_def_radek_vyska)+(3*d_def_okraj)>  //s gumpem width=480
//tag(uvodni_text,"<link.name>")
//tag(nabidka,"<tag(nabidka)>")
//tag(zajemce,"<tag(zajemce)>")
argo.settext(100,Stav drazby prodejniho mista "<?name?> - <?tag(housename)?>") //jméno linku (to je vendorplac)
argo.settext(101,Nabidka: <?tag(nabidka)?>)
argo.settext(102,Zajemce s posledni nabidkou: <?tag(zajemce).name?>)

//argo.htmlgump(120,120,450,30,0,0,100) //uvodni text
argo.text(120,120,955,100) //uvodni text
argo.text(140,145,955,101) //text 1  s gumpem y=155
argo.text(140,165,955,102) //text 2  s gumpem y=175
if(<?tag.drazba?>)
  argo.tag(cas,<f_vend_cas_do_konce_drazby>)
  argo.settext(103,Cas do konce drazby: <argo.tag(cas)>) //text 3
  argo.text(140,185,955,103) //text pri drazbe s gumpem y=195
else
  argo.settext(104,Cas do konce drazby: Drazba neni zahajena) //text 4
  argo.text(140,185,955,104) //text kdyz drazba neni s gumpem y=195
endif

[function f_vend_cas_do_konce_drazby]
if(finduid(<more1>).isgm)
 arg(celkem,6048000)
else
 arg(celkem,18144000)
endif
arg(zbyva_casu,<eval ((<tag(zacatekdrazby)>+<celkem>)-<serv.time>)>) //celkova doba - 3 nebo 1(majitelem GM) tydny(en) co trvala drazba
arg(dnu,<eval (arg(zbyva_casu)/864000)>) //den ma 86400s (*10 - decisekundy)
arg(hodin,<eval ((arg(zbyva_casu)-(arg(dnu)*864000))/36000)>) //hodina ma 3600s
arg(minut,<eval (((arg(zbyva_casu)-(arg(dnu)*864000))-(arg(hodin)*36000))/600)>)
arg(sekund,<eval ((((arg(zbyva_casu))-(arg(dnu)*864000))-(arg(hodin)*36000))-(arg(minut)*600))/10>)
return(<arg(dnu)/10> dni <arg(hodin)/10> hodin <arg(minut)/10> minut <arg(sekund)/10> sekund)

[eof]