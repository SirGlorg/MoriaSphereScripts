[EVENTS e_npc_wandering]
on=@Created
tag(wandering,0)
tag(wandering_length_way,0)
tag(wandering_way,0)

on=@NPCSeeNewPlayer
action=06d

on=@skillstroke
on=@hittry
on=@DrinkingPotion
on=@afterswing
on=@beforedoeffect 
on=@beforegeteffect
on=@beforegetswing
on=@aftergetswing

on=@UserDclick
if(src.isgm)
  if(<eval tag(wandering)>)
    dialog d_npc_wandering
  else
    tag(wandering,1)
    tag(wandering_length_way,0)
    tag(wandering_way,0)
    src.sysmessage("Na <name>, deklarovan wandering.")
  endif
endif

//////////////////////////////////////
///////// DIALOG WANDERING   /////////
//////////////////////////////////////

[dialog d_npc_wandering]
argo.SetLocation=20,30
argo.tag(sirka,600)
argo.tag(vyska,94+<tag(wandering_length_way)>*22)

argo.dialog_prvni
arg(i,0)
while(i < <tag(wandering_length_way)>+3)
  argo.dialog_pozadi(<argo.tag(nexty)>,1,250,250) 
  arg(i,#+1)
endwhile

argo.dialog_zpruhledni

argo.textA(<argo.dialog_textpos(0,0)>,42,"Wandering NPC")
argo.button(<argo.dialog_textpos(0,2)>,0fa5,0fa7,1,0,1)

arg(i,0)
while(i < <tag(wandering_length_way)>)
  argo.textA(<argo.dialog_textpos(1+<i>,0)>,42,<i>)
  argo.textA(<argo.dialog_textpos(1+<i>,1)>,42,"<tag(wandering_way_<i>)>")
  if(<eval tag(wandering_way_now)> == <i>)
    argo.textA(<argo.dialog_textpos(1+<i>,2)>,42,"x")
  endif
  arg(i,#+1)
endwhile

argo.textA(<argo.dialog_textpos(1+<i>,0)>,42,"Opakovat trasu")
argo.checkBox(<argo.dialog_textpos(1+<i>,1)>,210,211,<tag(wandering_repeat)>,1)

argo.textA(<argo.dialog_textpos(2+<i>,0)>,42,"Spustit")
argo.button(<argo.dialog_textpos(2+<i>,1)>,0fa5,0fa7,1,0,2)

[dialog d_npc_wandering button]
on=@AnyButton
if (argn==1) 
  tag(wandering_way_<tag(wandering_way)>,"<src.p>")
  tag(wandering_way,<eval tag(wandering_way)>+1)
  tag(wandering_length_way,<eval tag(wandering_length_way)>+1)
  dialog d_npc_wandering
endif

if (argn==2)
  tag(wandering_way_now,0)
  wandering_nextPoint
endif
               
if (<argChk(1)>)
  tag(wandering_repeat,1)
else
  tag(wandering_repeat,0)
endif 

[ITEMDEF i_wandering_npc_point]
ID=0aec
type=0
NAME=NPC wandering

ON=@Create
color=021
attr=090

ON=@Step
if(src.isChar)
  if(<eval src.tag(wandering)>)
    if(tag(wandering_npc_uid) == <src>)
      src.wandering_nextPoint
      remove
    endif
  endif  
endif

[function wandering_nextPoint]
if(!<restest 1 i_npc_wandering_control_action>)
  newitemsafe(i_npc_wandering_control_action)
  lastnew.equip
endif
if(tag(wandering_way_now) < tag(wandering_length_way))
  home=<tag(wandering_way_<tag(wandering_way_now)>)>
  action=06d
  newitem(i_wandering_npc_point)
  lastnew.tag(wandering_npc_uid,<uid>)
  lastnew.p <tag(wandering_way_<tag(wandering_way_now)>)>
  tag(wandering_way_now,<tag(wandering_way_now)>+1)
else
  if(tag(wandering_repeat))
    tag(wandering_way_now,0)
    wandering_nextPoint
  endif
endif 

[ITEMDEF i_npc_wandering_control_action]
ID=i_memory
NAME=Control action
TYPE=T_EQ_SCRIPT

ON=@CREATE
ATTR=attr_invis|attr_decay

ON=@EQUIP
timer=3

ON=@TIMER
if (cont.action != 06d)
  cont.action = 06d
endif
timer = 3
return 1